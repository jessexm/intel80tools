loc80.pex
lit             'literally'

/* seek modes */
SEEKTELL        '0'
SEEKBCK         '1'
SEEKABS         '2'
SEEKFWD         '3'
SEEKEND         '4'

ERR2            '2' /* ILLEGAL AFTN ARGUMENT */
ERR3            '3' /* TOO MANY OPEN FILES */
ERR4            '4' /* INCORRECTLY SPECIFIED FILE */
ERR5            '5' /* UNRECOGNIZED DEVICE NAME */
ERR9            '9' /* DISK DIRECTORY FULL */
ERR12           '12' /* FILE IS ALREADY OPEN */
ERR13           '13' /* NO SUCH FILE */
ERR14           '14' /* WRITE PROTECTED */
ERR17           '17' /* NOT A DISK FILE */
ERR19           '19' /* ATTEMPTED SEEK ON NON-DISK FILE */
ERR20           '20' /* ATTEMPTED BACK SEEK TOO FAR */
ERR21           '21' /* CAN'T RESCAN */
ERR22           '22' /* ILLEGAL ACCESS MODE TO OPEN */
ERR23           '23' /* MISSING FILENAME */
ERR27           '27' /* ILLEGAL SEEK COMMAND */
ERR28           '28' /* MISSING EXTENSION */
ERR31           '31' /* CAN'T SEEK ON WRITE ONLY FILE */
ERR32           '32' /* CAN'T DELETE OPEN FILE */
ERR35           '35' /* SEEK PAST EOF */
ERR203          '203' /* INVALID SYNTAX */
ERR204          '204' /* PREMATURE EOF */
ERR208          '208' /* CHECKSUM ERROR */
ERR210          '210' /* INSUFFICIENT MEMORY */
ERR211          '211' /* RECORD TOO LONG */
ERR212          '212' /* ILLEGAL RELO RECORD */
ERR213          '213' /* FIXUP BOUNDS ERROR */
ERR218          '218' /* ILLEGAL RECORD FORMAT */
ERR224          '224' /* BAD RECORD SEQUENCE */
ERR225          '225' /* INVALID NAME */
ERR226          '226' /* NAME TOO LONG */
ERR227          '227' /* LEFT PARENTHESIS EXPECTED */
ERR228          '228' /* RIGHT PARENTHESIS EXPECTED */
ERR229          '229' /* UNRECOGNIZED CONTROL */
ERR233          '233' /* 'TO' EXPECTED */
ERR237          '237' /* COMMON NOT FOUND */
ERR238          '238' /* ILLEGAL STACK CONTENT RECORD */
ERR239          '239' /* NO MODULE HEADER RECORD */
ERR240          '240' /* PROGRAM EXCEEDS 64K */

/* Record types */
R$MODHDR        '2'
R$MODEND        '4'
R$MODDAT        '6'
R$LINNUM        '8'
R$MODEOF        '0EH'
R$ANCEST        '10H'
R$LOCDEF        '12H'
R$PUBDEF        '16H'
R$EXTNAM        '18H'
R$FIXEXT        '20H'
R$FIXLOC        '22H'
R$FIXSEG        '24H'
R$LIBLOC        '26H'
R$LIBNAM        '28H'
R$LIBDIC        '2AH'
R$LIBHDR        '2CH'
R$COMDEF        '2EH'

/* Segments */
SABS            '0'
SCODE           '1'
SDATA           '2'
SSTACK          '3'
SMEMORY         '4'
SRESERVED       '5'
SNAMED          '6'	/* through 254 */
SBLANK          '255'

/* Alignments  & Flags*/
AMASK           '0FH'
AABS            '0'
AUNKNOWN        '0'
AINPAGE         '1'
APAGE           '2'
ABYTE           '3'
FHASADDR        '080H'
FWRAP0          '40H'
FSEGSEEN        '10H'

/* Fixup type */
FLOW            '1'
FHIGH           '2'
FBOTH           '3'

TRUE            '0FFH'
FALSE           '0'

/* controls seen tags */
startSeen       'seen(0)'
stackSizeSeen   'seen(1)'
restart0Seen    'seen(2)'
MapSeen         'seen(3)'
PublicsSeen     'seen(4)'
SymbolsSeen     'seen(5)'
LinesSeen       'seen(6)'
PurgeSeen       'seen(7)'
moduleNameSeen  'seen(8)'


/* user types */
SPATH$T         'STRUCTURE (SPATH$T1,SPATH$T2)'
SPATH$T1        'deviceId BYTE, name(6) BYTE, ext(3) BYTE'
SPATH$T2        'deviceType BYTE, driveType BYTE'
DATAFRAG$T      'STRUCTURE (saddr ADDRESS, eaddr ADDRESS)'
SEGFRAG$T       'STRUCTURE (flags BYTE, seg BYTE, start ADDRESS, len ADDRESS)'
DATAFRAG$T      'STRUCTURE (saddr ADDRESS, eaddr ADDRESS)'
PAGE1$T         'STRUCTURE (state BYTE, mpage BYTE)'
PAGE2$T         'STRUCTURE (pageIdx BYTE, fileIdx BYTE)'
RECORD$T        'STRUCTURE (rectyp BYTE, reclen ADDRESS, record(1) BYTE)'

/* based vars */
cmdbuf          BS..cmd$p
cmdch           B..cmd$p
erec            B..erec$p
inRecord        "RECORD$T"..inRecord$p
lsout           "RECORD$T"..lsout$p
outRecord       "RECORD$T"..outRecord$p
pageTab1        "PAGE1$T"(256)..pageTab1$p
pageTab2        "PAGE2$T"(256)..pageTab2$p

$file(system.lib)
isis            P(B rectyp,A parameter$ptr)
memck           PA
rescan          P(A conn,A status$p)
spath           P(A path$p,A info$p,A status$p)

$file(isis.plm)
close           P(A conn,A status$p)
delete          P(A path$p,A status$p)
error           P(A error$num)
exit            P
open            P(A conn$p,A path$p,A access,A echo,A status$p)
read            P(A conn,A buff$p,A count,A actual$p,A status$p)
seek            P(A conn,A mode,A block$p,A byte$p,A status$p)
write           P(A conn,A buff$p,A count,A status$p)

$file(loc1.plm)
actRead         A
aRecordType     BS
baseMemImage    A
botHeap         A
columns         B
crlf            BS
curDataFrag$p   A
curSegFrag$p    A
eibuf$p         A
eout$p          A
epbuf$p         A
erec$p          A
havePagingFile  B
ibuf$p          A
inBlk           A
inb$p           A
inByt           A
inCRC           B
inFileName      BS
inFragment      "DATAFRAG$T"
in$p            A
inRecord$p      A
isMain          B
modhdr$x1       B
modhdr$x2       B
moduleName      BS
nextDataFrag$p  A
nextSegFrag$p   A
npbuf           A
nxtPageNo       B
outFileName     BS
out$p           A
outputfd        A
outRealFile     B
outRecord$p     A
pageCacheSize   B
pageIndexTmpFil B
pageTab1$p      A
pageTab2$p      A
pbuf$p          A
printfd         A
printFileName   BS
readfd          A
recLen          A
recNum          A
roundRobinIndex B
seen            BS
segBases        AS
segFlags        BS
segOrder        BS
segSizes        AS
sibuf$p         A
spbuf$p         A
startAddr       A
statusIO        A
tmpfd           A
tmpFileName     BS
topDataFrags    A
topHeap         A
usePrintBuf     B
VERSION         BS
addDataFrag     P(A saddr,A eaddr)
addrInCache     P(A addr)A
addSegFrag      P(B flags,B seg,A start,A len)
alloc           P(A cnt)
allocNewPage    P(B page)A
anotherPage     P(B page)
badRecordSeq    P
chkRead         P(A cnt)
conAndPrint     P(A buf,A cnt)
conStrOut       P(A buf,A cnt)
fatalErr        P(B errCode)
flushPrintBuf   P
getRecord       P
illegalRecord   P
illegalReloc    P
objSeek           P(A blk,A byt)
loadMODDAT      P(B segId)
pageout         P(B page,A bufp)
printcrlf       P
printString     P(A bufp,A cnt)
pstrcpy         P(A psrc,A pdst)
seekPagingFile  P(B para)

$file(loc2a.plm)
errmsg          P(A errCode)

$file(loc2b.plm)
alin            BS
aMod            BS
aPub            BS
aReadFromFile   BS
aReferenceToUns BS
aSym            BS
aSymbol         BS
aSymbolTableOfM BS
aUnsatisfiedExt BS
aValueType      BS
aWrittenToFile  BS
curcol          B
curColumn       A
curListField    A
loHiBoth        B
lsout$p         A
nameLen         B
outSegType      B
spc32           BS
unsatisfiedCnt  A
workingSegBase  A
x5              BS
emitMODDAT      P(A curDataFrag$p)
endRecord       P
flushOut        P
forceSOL        P
initRecOut      P(B rectyp)
printColumn     P(A field,A pstr)
procMODDAT      P
procMODEND      P
seekOutFile     P(A mode,A pblk,A pbyt)
setWorkingSeg   P(B seg)B
writeBytes      P(A buf$p,A cnt)

$file(loc3.plm)
fixupBoundsChk  P(A addr)
locateFile      P
printListingHeader P(A buf,A len)
procANCEST      P
procDefs        P(B list,A template)
procEXTNAM      P
procLINNUM      P

$file(loc4.plm)
a0LengthSegment BS
aAddresses      BS
alignNames      BS
aMemoryMapOfMod BS
aMemOverlap     BS
aModuleIsNotAMa BS
aModuleStartAdd BS
aRestartControl BS
aStartControlIg BS
aStartStopLengt BS
segNames        BS
getCommonName   P(B segid)A
printMemoryMap  P
skipspc         P(A pch)A

$file(loc4a.plm)
errChkReport    P(A errCode,A file,B errExit)

$file(loc5a.plm)
binasc          P(A number,B base,B pad,A bufp,B ndigits)
pastAFN         P(A pch)A

$file(loc5b.plm)
pastFileName    P(A pch)A

$file(loc6.plm)
aCommandTailErr BS
aInvokedBy      BS
cin             BS
cmd$p           A
controls        BS
cout            BS
mdebug          BS
mstar2          BS
mto             BS
mtoand          BS
scmd$p          A
signonMsg       BS
spathInfo       "SPATH$T"
tmpFileInfo     BS
cmdErr          P(A err)
errNotADisk     P
expectChar      P(B ch,B err)
expectLP        P
expectRP        P
expectSlash     P
getFile         P
getpstrName     P(A pstr)
initSegOrder    P
processControls P
skipNonArgChars P(A pch)

$file(loc7.plm)
aInpageSegment2 BS
nxtSegOrder     B
pad7935         BS
segId           B
getCommonSegId  PB
insSegIdOrder   P(B seg)
parseLPNumRP    PA
procArgs$Init   P
procCOMDEF      P
procHdrAndComDef P
procMODHDR      P
readCmdLine     P

$file(loc8.plm)
makeFullName    P(A pinfo,A pstr)
strupr          P(A pch)

$file(loc8a.plm)
parseNumber     P(A ppstr)A
strequ          P(A pstr1,A pstr2,B len)B

$file(loc9.plm)
toupper         P(B ch)B

$file(memmov.asm)
mem$mov         P(A cnt,A src,A dst)

isis.plm
isissub:
DO;
DECLARE LIT LITERALLY 'LITERALLY',
	IOPEN LIT	'0',
	ICLOSE LIT	'1',
	IDELETE LIT	'2',
	IREAD LIT	'3',
	IWRITE LIT	'4',
	ISEEK LIT	'5',
	ILOAD LIT	'6',
	IRENAME LIT	'7',
	ICONSOL LIT	'8',
	IEXIT LIT	'9',	
	IATTRIB LIT	'10',
	IRESCAN LIT	'11',
	IERROR LIT	'12',
	IWHOCON LIT	'13',
	ISPATH LIT	'14';

$include(isis.ipx)

close: procedure (conn, status$p) public;
    declare (conn, status$p) address;
    CALL ISIS(ICLOSE, .conn);
end close;

delete: procedure (path$p, status$p) public;
    declare (path$p, status$p) address;
    CALL ISIS(IDELETE, .path$p);
end delete;

error: procedure (error$num) public;
    declare (error$num) address;
    declare err address;
    err = .err;
    CALL ISIS(IERROR, .error$num);
end error;

exit: procedure public;
	DECLARE exitarg ADDRESS;
	exitarg = .exitarg;
	CALL ISIS(IEXIT, .exitarg);
end exit;


open: procedure (conn$p, path$p, access, echo, status$p) public;
    declare (conn$p, path$p, access, echo, status$p) address;
    CALL ISIS(IOPEN, .conn$p);
end open;


read: procedure (conn, buff$p, count, actual$p, status$p) public;
    declare (conn, buff$p, count, actual$p, status$p) address;
    CALL ISIS(IREAD, .conn);
end read;

seek: procedure (conn, mode, block$p, byte$p, status$p) public;
    declare (conn, mode, block$p, byte$p, status$p) address;
    CALL ISIS(ISEEK, .conn);
end seek;

write: procedure (conn, buff$p, count, status$p) public;
    declare (conn, buff$p, count, status$p) address;
    CALL ISIS(IWRITE, .conn);
end write;



END;
loc1.plm
$LIST
$CODE
LOCATE:
DO;
$include(loc1.ipx)

DECLARE COPYRIGHT (*) BYTE DATA ('(C) 1976, 1977, 1979 INTEL CORP');
DECLARE VERSION (*) BYTE PUBLIC DATA ('V3.0');
DECLARE NULLREC (*) BYTE DATA (0,0,0);
DECLARE pageoutOffset ADDRESS DATA (0);
DECLARE pageoutOffset$b STRUCTURE (lobyt BYTE, hibyt BYTE) AT (.pageoutOffset);
DECLARE cachePageOffset ADDRESS DATA (0);
DECLARE cachePageOffset$b STRUCTURE (lobyt BYTE, hibyt BYTE) AT (.cachePageOffset);


DECLARE (readfd, outputfd, printfd, tmpfd, statusIO, actRead) ADDRESS PUBLIC,
	inFileName (16) BYTE PUBLIC, outFileName (16) BYTE PUBLIC,
	printFileName (16) BYTE PUBLIC, tmpFileName (16) BYTE PUBLIC,
	columns BYTE PUBLIC,
	seen(9) BYTE PUBLIC,	/* START, STACK, RESTART0, MAP, PUBLICS, SYMBOLS, LINES, PURGE, NAME */
	moduleName(32) BYTE PUBLIC,
	isMain BYTE PUBLIC,
	modhdr$x1 BYTE PUBLIC,
	modhdr$x2 BYTE PUBLIC,
	startAddr ADDRESS PUBLIC,
	segOrder(255) BYTE PUBLIC,
	segBases(256) ADDRESS PUBLIC,	/* abs, code, data */
	segSizes(256) ADDRESS PUBLIC,
	segFlags(256) BYTE PUBLIC,
	inRecord$p ADDRESS PUBLIC,
	erec$p ADDRESS PUBLIC,
	in$p ADDRESS PUBLIC,
	recNum ADDRESS PUBLIC,
	recLen ADDRESS PUBLIC,
	npbuf ADDRESS PUBLIC,
	sibuf$p ADDRESS PUBLIC,
	ibuf$p ADDRESS PUBLIC,
	eibuf$p ADDRESS PUBLIC,
	outRecord$p ADDRESS PUBLIC,
	out$p ADDRESS PUBLIC,
	eout$p ADDRESS PUBLIC,
	spbuf$p ADDRESS PUBLIC,
	pbuf$p ADDRESS PUBLIC,
	epbuf$p ADDRESS PUBLIC,
	topHeap ADDRESS PUBLIC,
	topDataFrags ADDRESS PUBLIC,
	curSegFrag$p ADDRESS PUBLIC,
	nextSegFrag$p ADDRESS PUBLIC,
	botHeap ADDRESS PUBLIC,
	curDataFrag$p ADDRESS PUBLIC,
	nextDataFrag$p ADDRESS PUBLIC,
	inFragment DATAFRAG$T PUBLIC,
	usePrintBuf BYTE PUBLIC,
	outRealFile BYTE PUBLIC,
	baseMemImage ADDRESS PUBLIC,
	havePagingFile BYTE PUBLIC,
	pageCacheSize BYTE PUBLIC,
	roundRobinIndex BYTE PUBLIC,
	nxtPageNo BYTE PUBLIC,
	pageIndexTmpFil BYTE PUBLIC,
	pageTab1$p ADDRESS PUBLIC,
	pageTab2$p ADDRESS PUBLIC;

DECLARE	crlf(2) BYTE PUBLIC INITIAL(0DH, 0AH),
	aRecordType(*) BYTE PUBLIC INITIAL(' RECORD TYPE XXH, RECORD NUMBER *****', 0Dh, 0Ah),
	(inBlk, inByt) ADDRESS PUBLIC INITIAL(0,0),
	inb$p ADDRESS PUBLIC,
	inCRC BYTE PUBLIC;

/* explicit based vars as base is defined after include */
declare	inRecord based inRecord$p RECORD$T,
	pageTab1 based pageTab1$p (256) PAGE1$T,
	pageTab2 based pageTab2$p (256) PAGE2$T;

conStrOut: PROCEDURE(buf, cnt) PUBLIC;
	DECLARE (buf, cnt) ADDRESS;
	CALL write(0, buf, cnt, .statusIO);
END conStrOut;


flushPrintBuf: PROCEDURE PUBLIC;
	IF usePrintBuf THEN	/* only process if buffered print */
	DO;
		CALL write(printfd, spbuf$p, pbuf$p - spbuf$p, .statusIO);
		CALL errChkReport(statusIO, .printFileName(1), TRUE);
	END;
	pbuf$p = spbuf$p;	/* rest buffer pointer */
END flushPrintBuf;


fatalErr: PROCEDURE(errCode) PUBLIC;
	DECLARE errCode BYTE;

	CALL flushPrintBuf;
	CALL conStrOut(.(' '), 1);
	CALL conStrOut(.inFileName(1), inFileName(0));
	CALL conStrOut(.(','), 1);
	CALL errmsg(errCode);
	CALL binasc(inRecord.rectyp, 16, '0', .aRecordType(13), 2);
	IF recNum > 0 THEN
		CALL binasc(recNum, 10, ' ', .aRecordType(32), 5);
	CALL conStrOut(.aRecordType, 39); 
	CALL exit;
END fatalErr;


illegalRecord: PROCEDURE PUBLIC;
	CALL fatalErr(ERR218);	/* Illegal Record Format */
END illegalRecord;


illegalReloc: PROCEDURE PUBLIC;
	CALL fatalErr(ERR212);	/* Illegal Relo Record */
END illegalReloc;


badRecordSeq: PROCEDURE PUBLIC;
	CALL fatalErr(ERR224);	/* Bad Record Sequence */
END badRecordSeq;


pstrcpy: PROCEDURE(psrc, pdst) PUBLIC;	/* copy pascal style string */
	DECLARE (psrc, pdst) ADDRESS;
	DECLARE src BASED psrc (1) BYTE;
	
	CALL mem$mov(src(0) + 1, psrc, pdst);
END pstrcpy;


printString: PROCEDURE(bufp, cnt) PUBLIC;
	DECLARE bufp ADDRESS;
	DECLARE cnt ADDRESS;
	DECLARE bcnt ADDRESS;

	IF usePrintBuf THEN	/* using buffer ? */
	DO;
		bcnt = epbuf$p - pbuf$p; 	/* space in buffer */
		DO WHILE cnt > bcnt;		/* not enough room for all chars */
			CALL mem$mov(bcnt, bufp, pbuf$p);	/* copy what we can */
			cnt = cnt - bcnt;	/* adjust what is left */
			bufp = bufp + bcnt;	/* advance position in the src string */
			pbuf$p = pbuf$p + bcnt;	/* and the print buffer position */
			CALL flushPrintBuf;	/* write the buffer */
			bcnt = npbuf;		/* reset to the buffer size */
		END;
		CALL mem$mov(cnt, bufp, pbuf$p); /* copy the remaining chars */
		pbuf$p = pbuf$p + cnt;		/* advance the print buf ptr */
	END;
	ELSE
	DO;	/* just use the operating system functions */
		CALL write(printfd, bufp, cnt, .statusIO);
		CALL errChkReport(statusIO, .printFileName(1), TRUE);
	END;
END printString;


printcrlf: PROCEDURE PUBLIC;
	CALL printString(.crlf, 2);
END printcrlf;


conAndPrint: PROCEDURE(buf, cnt) PUBLIC;
	DECLARE (buf, cnt) ADDRESS;
	CALL printString(buf, cnt);
	IF printfd > 0 THEN	/* make sure always seen on console */
		CALL conStrOut(buf, cnt);
END conAndPrint;


chkRead: PROCEDURE(cnt) PUBLIC;
	DECLARE cnt ADDRESS;
	DECLARE bcnt ADDRESS;

	IF (bcnt := eibuf$p - ibuf$p) < cnt THEN	/* data already in buffer */
	DO;
		CALL mem$mov(bcnt, ibuf$p, sibuf$p);	/* move down bytes in buffer */
		CALL read(readfd, sibuf$p + bcnt, npbuf - bcnt, .actRead, .statusIO);
		CALL errChkReport(statusIO, .inFileName(1), TRUE);
		inBlk = inBlk + (inByt + ibuf$p - sibuf$p) / 128;
		inByt = (inByt + ibuf$p - sibuf$p) MOD 128;
		IF (bcnt := bcnt + actRead) < cnt THEN
			CALL errChkReport(ERR204, .inFileName(1), TRUE);  /* Premature EOF */
		eibuf$p = (ibuf$p := sibuf$p) + bcnt;
	END;
END chkRead;

getRecord: PROCEDURE PUBLIC;
	DECLARE ch BASED inb$p BYTE;
	DECLARE bcnt ADDRESS;
	DECLARE erec BASED erec$p BYTE;

	IF (bcnt := eibuf$p - ibuf$p) >= 4 THEN
	DO;
		inRecord$p = ibuf$p;
		erec$p = inRecord$p + inRecord.reclen + 2;
	END;
	ELSE
	DO;
		inRecord$p = .NULLREC;	/* scratch area */
		erec$p = 0FFFFH;
	END;
	IF erec$p >= eibuf$p THEN
	DO;
		IF inRecord.reclen <= 1025 THEN
		DO;
			CALL chkRead(bcnt + 1);
			inRecord$p = ibuf$p;
			IF (erec$p := inRecord$p + inRecord.reclen + 2) >= eibuf$p THEN
				CALL errChkReport(ERR204, .inFileName(1), TRUE);	/* premature EOF */
		END;
	END;
	recLen = inRecord.reclen;
	in$p = inRecord$p + 3;
	ibuf$p = erec$p + 1;	/* update for next record */
	recNum = recNum + 1;
	IF inRecord.rectyp > R$COMDEF OR inRecord.rectyp THEN	/* > 2EH or odd */
		CALL illegalReloc;
	IF inRecord.rectyp = R$MODDAT THEN
		RETURN;
	IF recLen > 1025 THEN
		CALL fatalErr(ERR211);	/* Record too long */
	inCRC = 0;
	DO inb$p = inRecord$p TO .erec;
		inCRC = inCRC + ch;
	END;
	IF inCRC <> 0 THEN
		CALL fatalErr(ERR208);	/* checksum error */
END getRecord;


objSeek: PROCEDURE(blk, byt) PUBLIC;
	DECLARE (blk, byt) ADDRESS;

	recNum = 0;		/* reset record number */
	IF  inBlk <= blk AND blk <= inBlk + (inByt + (eibuf$p - sibuf$p)) / 128 THEN
		IF (ibuf$p := sibuf$p + (blk - inBlk) * 128 + (byt - inByt)) >= sibuf$p
		    AND ibuf$p < eibuf$p THEN
			RETURN;
	CALL seek(readfd, SEEKABS, .blk, .byt, .statusIO);
	CALL errChkReport(statusIO, .inFileName(1), TRUE);
	ibufp = eibufp;
	CALL chkRead(1);	/* read the buffer */
	inBlk = blk;
	inByt = byt;
END objSeek;



seekPagingFile: PROCEDURE(para) PUBLIC;
	DECLARE para BYTE;
	DECLARE blk ADDRESS;
	
	blk = DOUBLE(para) + DOUBLE(para);
	CALL seek(tmpfd, SEEKABS, .blk, .(0,0), .statusIO);
	CALL errChkReport(statusIO, .tmpFileName(1), TRUE);
END seekPagingFile;


pageout: PROCEDURE(page, bufp) PUBLIC;
	DECLARE page BYTE;
	DECLARE bufp ADDRESS;
	DECLARE pt ADDRESS, t BASED pt STRUCTURE (pageIdx BYTE, fileIdx BYTE);

	pt = .pageTab2(page);
	IF t.fileIdx = 0FFH THEN	/* not used */
	DO;
		t.fileIdx = pageIndexTmpFil;	/* record location in tmp file */
		pageIndexTmpFil = pageIndexTmpFil + 1;
	END;
	CALL seekPagingFile(t.fileIdx);
	CALL write(tmpfd, bufp, 256, .statusIO);	/* write the page out */
	CALL errChkReport(statusIO, .tmpFileName(1), TRUE);
	t.pageIdx = 0FEH;	/* flag as paged out */
END pageout;


anotherPage: PROCEDURE(page) PUBLIC;
	DECLARE page BYTE;
	DECLARE i BYTE;
	

	IF usePrintBuf THEN	/* use print buffer space */
	DO;
		CALL flushPrintBuf;	/* flush the buffer */
		usePrintBuf = FALSE;	/* no longer available */
		/* move the heap up */
		CALL mem$mov(topHeap - botHeap, botHeap, botHeap + npbuf);
		/* adjust the topheap and the start/cur print buf addresses */
		spbuf$p, pbuf$p, topheap = epbuf$p;
		/* adjust the descriptor pointers */
		topDataFrags = topDataFrags + npbuf;
		curSegFrag$p = curSegFrag$p + npbuf;
		curDataFrag$p = curDataFrag$p + npbuf;
		/* page allocated is first after the current max pageCacheSize */
		nxtPageNo = pageCacheSize + 1;
		/* increase the pageCacheSize available */
		pageCacheSize = HIGH((botHeap := botHeap + npbuf) - baseMemImage) - 1;
		RETURN;
	END;
	IF NOT havePagingFile THEN	/* have to page, create file if not done */
	DO;
		havePagingFile = TRUE;
		CALL delete(.tmpFileName(1), .statusIO);
		CALL open(.tmpfd, .tmpFileName(1), 3, FALSE, .statusIO);
		CALL errChkReport(statusIO, .tmpFileName(1), TRUE);
	END;

	pageTab1(page).state = 0FFH;	/* mark this page as now free */
	i = pageTab1(page).mpage;	/* get the current target page stored here */
	pageoutOffset$b.hibyt = page;
	CALL pageout(i, baseMemImage + pageoutOffset);
END anotherPage;


allocNewPage: PROCEDURE(page) ADDRESS PUBLIC;
	DECLARE page BYTE;

	DO nxtPageNo = 0 TO pageCacheSize;		/* see if free page */
		IF pageTab1(nxtPageNo).state THEN	/* one is free */
			GOTO found;
	END;
	IF roundRobinIndex > pageCacheSize THEN		/* manage roundRobin for paging to disk */
		roundRobinIndex = 0;
	nxtPageNo = roundRobinIndex;
	CALL anotherPage(roundRobinIndex);		/* get a page */
	IF nxtPageNo = roundRobinIndex  THEN	/* if the roundRobin page then we didn't get from printbuf reuse */
		roundRobinIndex = roundRobinIndex + 1;	/* so manage the roundRobin counter */
found:
	pageTab1(nxtPageNo).state = 0;	/* no longer free */
	pageTab1(nxtPageNo).mpage = page;	/* record the memory page located here */
	pageTab2(page).pageIdx = nxtPageNo;	/* save the mapping */
	cachePageOffset$b.hibyt = nxtPageNo;
	RETURN baseMemImage + cachePageOffset;		/* return the pointer in memory */
END allocNewPage;


/* convert a target address into its current location in cache */
addrInCache: PROCEDURE(addr) ADDRESS PUBLIC;
	DECLARE addr ADDRESS;
	DECLARE baddr ADDRESS;
	DECLARE (lobyt, hibyt) BYTE AT(.baddr);
	DECLARE pt ADDRESS;
	DECLARE t BASED pt STRUCTURE (pageIdx BYTE, fileIdx BYTE);

	pt = .pageTab2(HIGH(addr));
	IF t.pageIdx = 0FFH THEN	/* needs allocating */
		RETURN allocNewPage(HIGH(addr)) + LOW(addr);
	IF t.pageIdx = 0FEH THEN	/* on disk */
	DO;
		baddr = allocNewPage(HIGH(addr));	/* alloc page in cache */
		CALL seekPagingFile(t.fileIdx);		/* seek to its place in the paging file */
		CALL read(tmpfd, baddr, 256, .actRead, .StatusIO);	/* read in */
		CALL errChkReport(statusIO, .tmpFileName(1), TRUE);
		IF actRead <> 256 THEN			/* if problems and not :BB: out file then error */
			IF outRealFile THEN
				CALL errChkReport(ERR204, .tmpFileName(1), TRUE);    /* premature EOF */
		RETURN baddr + LOW(addr);		/* return the mapped address */
	END;
	hibyt = t.pageIdx;		/* page offset in cache */
	lobyt = LOW(addr);		/* offset in page */
	RETURN baseMemImage + baddr;	/* mapped address */
END addrInCache;


alloc: PROCEDURE(cnt) PUBLIC;
	DECLARE cnt ADDRESS;

	/* allocate but check whether we have crossed a page boundary */
	IF HIGH(botHeap - baseMemImage) <> HIGH((botHeap := botHeap - cnt) - baseMemImage) THEN
	DO;
		IF NOT pageTab1(pageCacheSize).state THEN	/* if current cache all used, try forcing using print buf */
			CALL anotherPage(PageCacheSize);	/* or paging the last page to disk */

		IF (pageCacheSize := HIGH(botHeap - baseMemImage) - 1) = 0FFH THEN	/* check we haven't eliminated all cache */
			CALL errChkReport(ERR210, .inFileName(1), TRUE);	/* Insufficient memory */
	END;
END alloc;


addSegFrag: PROCEDURE(flags, seg, start, len) PUBLIC;
	DECLARE (flags, seg) BYTE;
	DECLARE (start, len) ADDRESS;
	DECLARE curSegFrag BASED curSegFrag$p SEGFRAG$T;
	DECLARE nextSegFrag BASED nextSegFrag$p SEGFRAG$T;

	curSegFrag$p = topHeap;

	DO WHILE curSegFrag$p <> topDataFrags;
		curSegFrag$p = curSegFrag$p - size(curSegFrag);
		IF curSegFrag.start > start THEN
			IF start + len = curSegFrag.start AND curSegFrag.seg = seg THEN /* does the new frag join the curfrag */
			DO;	/* merge the blocks */
				curSegFrag.start = start;
				curSegFrag.len = curSegFrag.len + len;
				RETURN;
			END;
			ELSE
				GOTO break;
		ELSE IF curSegFrag.start + curSegFrag.len = start THEN	/* does the curFrag join the new frag */
			IF curSegFrag.seg = seg THEN			/* segs must be same */
			DO;
				curSegFrag.len = curSegFrag.len + len;	/* merge the lengths */
				IF curSegFrag$p <> topDataFrags THEN	/* if not at the end of list see if this joins the next one */
				DO;
					nextSegFrag$p = curSegFrag$p - size(nextSegFrag);
					IF nextSegFrag.start = curSegFrag.start + curSegFrag.len AND nextSegFrag.seg = curSegFrag.seg THEN
					DO;
						curSegFrag.len = curSegFrag.len + nextSegFrag.len;	/* join and delete extra frag */
						CALL mem$mov(nextSegFrag$p - botHeap, botHeap, botHeap + size(nextSegFrag));
						topDataFrags = topDataFrags + size(curSegFrag);
						botHeap = botHeap + size(curSegFrag);
					END;
				END;
				RETURN;
			END;
	END;
	curSegFrag$p = curSegFrag$p - size(curSegFrag);	/* will insert at end of list */
break:
	CALL alloc(6);	/* reserve space for the segFrag */
	topDataFrags = topDataFrags - size(curSegFrag);
	CALL mem$mov(curSegFrag$p - botHeap, botHeap + size(curSegFrag), botHeap);	/* create the space in the list */
	curSegFrag.flags = flags;	/* set all of the values */
	curSegFrag.seg = seg;
	curSegFrag.start = start;
	curSegFrag.len = len;
END addSegFrag;


addDataFrag: PROCEDURE(saddr, eaddr) PUBLIC;
	DECLARE saddr ADDRESS;
	DECLARE eaddr ADDRESS;
	DECLARE curDataFrag BASED curDataFrag$p DATAFRAG$T;
	DECLARE nextDataFrag BASED nextDataFrag$p DATAFRAG$T;

	curDataFrag$p = topDataFrags;
	DO WHILE curDataFrag$p <> botHeap;
		curDataFrag$p = curDataFrag$p - size(curDataFrag);
		IF curDataFrag.saddr > saddr THEN	/* the curfrag starts above this one */
		DO;
			IF eaddr >= curDataFrag.saddr - 1 THEN	/* does the new frag join or overlap the curent one */
			DO;
				curDataFrag.saddr = saddr;		/* update the start */
				IF eaddr > curDataFrag.eaddr THEN	/* it overlaps */
				DO;
					curDataFrag.eaddr = eaddr;	/* extend the existing */
					nextDataFrag$p = curDataFrag$p - size(curDataFrag);	/* check if more are overlapped */
					DO WHILE curDataFrag$p <> botHeap;
						IF eaddr + 1 < nextDataFrag.saddr THEN	/* no there is a gap all done */
							RETURN;
						IF eaddr + 1 = nextDataFrag.saddr THEN	/* joins so update the end address */
							curDataFrag.eaddr = nextDataFrag.eaddr;
						/* remove the other descriptor if either joined or was overlapped */
						CALL mem$mov(nextDataFrag$p - botHeap, botHeap, botHeap + size(curDataFrag));
						botHeap = botHeap + size(nextDataFrag);
					END;
				END;
				RETURN;
			END;
			ELSE	/* we have a new fragment */
			DO;
				CALL alloc(4);	/* alloc more space and insert descriptor above this one */
				CALL mem$mov(curDataFrag$p - botHeap, botHeap + size(curDataFrag), botHeap);
				curDataFrag.saddr = saddr;
				curDataFrag.eaddr = eaddr;
				RETURN;
			END;
		END;
		ELSE	/* check if candidate frag to join / overlap */
		DO;
			IF curDataFrag.saddr = saddr OR saddr - 1 <= curDataFrag.eaddr THEN
			DO;
				IF curDataFrag.eaddr >= eaddr THEN	/* already overlapped */
					RETURN;
				curDataFrag.eaddr = eaddr;		/* set new end address */
				nextDataFrag$p = curDataFrag$p - size(curDataFrag);	/* delete other join/overlapping frags */
				DO WHILE curDataFrag$p <> botHeap;
					IF nextDataFrag.saddr - 1 > eaddr THEN
						RETURN;
					IF nextDataFrag.saddr - 1 = eaddr THEN
						curDataFrag.eaddr = nextDataFrag.eaddr;
					CALL mem$mov(nextDataFrag$p - botHeap, botHeap, botHeap + size(curDataFrag));
					botHeap = botHeap + size(curDataFrag);
				END;
				RETURN;
			END;
		END;	/* IF ELSE */
	END;	/* DO WHILE */

	CALL alloc(4);
	curDataFrag$p = botHeap;	/* allocated at new botHeap */
	curDataFrag.saddr = saddr;	/* record the start and end address */
	curDataFrag.eaddr = eaddr;
END addDataFrag;


loadMODDAT: PROCEDURE(segId) PUBLIC;
	DECLARE segId BYTE;
	DECLARE rdata BASED in$p (1) BYTE;
	DECLARE (dataLen, bytes2Read, curLoadAddr) ADDRESS;
	DECLARE curLoadPage BYTE;

	DECLARE ch BASED inb$p BYTE;

	dataLen = recLen - 4;
	inCRC = HIGH(recLen) + 6 + LOW(recLen) + rdata(0) + rdata(1) + rdata(2);
	ibuf$p = ibuf$p + 3;
	IF LOW(curLoadAddr := inFragment.saddr) <> 0 THEN
	DO;
		IF HIGH(curLoadAddr + (bytes2Read := dataLen) - 1) <> HIGH(curLoadAddr) THEN
			bytes2Read = 256 - LOW(curLoadAddr);	/* don't go over page boundary */
		CALL chkRead(bytes2Read);			/* make sure data is in the buffer */
		DO inb$p = ibuf$p TO ibuf$p + bytes2Read - 1;	/* add the data to the crc */
			inCRC = inCRC + ch;
		END;
		CALL mem$mov(bytes2Read, ibuf$p, addrInCache(curLoadAddr));	/* copy to the pagecache location */
		dataLen = dataLen - bytes2Read;			/* adjust bytes read */
		curLoadAddr = curLoadAddr + bytes2Read;
		ibuf$p = ibuf$p + bytes2Read;
	END;
	DO WHILE dataLen >= 256;				/* read in whole pages */
		CALL chkRead(256);				/* make sure data in buffer */
		DO inb$p = ibuf$p TO ibuf$p + 255;		/* add to crc */
			inCRC = inCRC + ch;
		END;
		IF segId = SABS AND havePagingFile THEN		/* if abs and paging write it out to paging file */
								/* there is no fixup involved */
		DO;
			/* if a page cache entry exists then this would be trashed anyway so free up */
			IF pageTab2(curLoadPage := HIGH(curLoadAddr)).pageIdx <= 0FDH THEN
				pageTab1(pageTab2(curLoadPage).pageIdx).state = 0FFH;
			CALL pageout(curLoadPage, ibuf$p);
		END;
		ELSE						/* copy to the page cache */
			CALL mem$mov(256, ibuf$p, addrInCache(curLoadAddr));

		dataLen = dataLen - 256;			/* adjust and continue */
		curLoadAddr = curLoadAddr + 256;
		ibuf$p = ibuf$p + 256;
	END;
	IF dataLen > 0 THEN					/* load an remaining into the page cache */
	DO;
		CALL chkRead(dataLen);
		DO inb$p = ibuf$p TO ibuf$p + dataLen - 1;
			inCRC = inCRC + ch;
		END;
		CALL mem$mov(dataLen, ibuf$p, addrInCache(curLoadAddr));
		ibuf$p = ibuf$p + dataLen;
	END;
	CALL chkRead(1);
	ibuf$p = (inb$p := ibuf$p) + 1;
	IF inCRC + ch <> 0 THEN					/* sanity check */
		CALL fatalErr(ERR208);	/* Checksum error */
END loadMODDAT;

main:
	baseMemImage, pageTab2$p = .controls;	/* once we have processed args overwrite area */
	CALL procArgs$Init;
	CALL locateFile;
	CALL flushPrintBuf;
	CALL close(printfd, .statusIO);
	CALL exit;

END;
loc2a.plm
locate2a: DO;

$include(loc2a.ipx)

DECLARE	errTab(*) BYTE DATA (
	   2, 'ILLEGAL AFTN ARGUMENT', 0,
	   3, 'TOO MANY OPEN FILES', 0,
	   4, 'INCORRECTLY SPECIFIED FILE', 0,
	   5, 'UNRECOGNIZED DEVICE NAME', 0,
	   9, 'DISK DIRECTORY FULL', 0,
	 0Ch, 'FILE IS ALREADY OPEN', 0,
	 0Dh, 'NO SUCH FILE', 0,
	 0Eh, 'WRITE PROTECTED', 0,
	 11h, 'NOT A DISK FILE', 0,
	 13h, 'ATTEMPTED SEEK ON NON-DISK FILE', 0,
	 14h, 'ATTEMPTED BACK SEEK TOO FAR', 0,
	 15h, 'CAN''T RESCAN', 0,
	 16h, 'ILLEGAL ACCESS MODE TO OPEN', 0,
	 17h, 'MISSING FILENAME', 0,
	 1Bh, 'ILLEGAL SEEK COMMAND', 0,
	 1Ch, 'MISSING EXTENSION', 0,
	 1Fh, 'CAN''T SEEK ON WRITE ONLY FILE', 0,
	 20h, 'CAN''T DELETE OPEN FILE', 0,
	 23h, 'SEEK PAST EOF', 0,
	0CBh, 'INVALID SYNTAX', 0,
	0CCh, 'PREMATURE EOF', 0,
	0D0h, 'CHECKSUM ERROR', 0,
	0D2h, 'INSUFFICIENT MEMORY', 0,
	0D3h, 'RECORD TOO LONG', 0,
	0D4h, 'ILLEGAL RELO RECORD', 0,
	0D5h, 'FIXUP BOUNDS ERROR', 0,
	0DAh, 'ILLEGAL RECORD FORMAT', 0,
	0E0h, 'BAD RECORD SEQUENCE', 0,
	0E1h, 'INVALID NAME', 0,
	0E2h, 'NAME TOO LONG', 0,
	0E3h, 'LEFT PARENTHESIS EXPECTED', 0,
	0E4h, 'RIGHT PARENTHESIS EXPECTED', 0,
	0E5h, 'UNRECOGNIZED CONTROL', 0,
	0E9h, '''TO'' EXPECTED', 0,
	0EDh, 'COMMON NOT FOUND', 0,
	0EEh, 'ILLEGAL STACK CONTENT RECORD', 0,
	0EFh, 'NO MODULE HEADER RECORD', 0,
	0F0h, 'PROGRAM EXCEEDS 64K', 0);

errmsg: PROCEDURE(errCode) PUBLIC;
	DECLARE errCode ADDRESS;
	DECLARE i ADDRESS;
	DECLARE status ADDRESS;
	
	IF LOW(errCode) <> 0 THEN
	DO;
		i = 0;
		DO WHILE i < LENGTH(errTab);
			IF LOW(errCode) = errTab(i) THEN
			DO;
				CALL write(0, .(' '), 1, .status);
				DO WHILE errTab(i := i + 1) <> 0;
					CALL write(0, .errTab(i), 1, .status);
				END;
				CALL write(0, .(0DH, 0AH), 2, .status);
				RETURN;
			END;
			ELSE
			DO;
				DO WHILE errTab(i := i + 1) <> 0;
				END;
				i = i + 1;
			END;
		END;
		CALL error(errCode);	/* ISIS error code */
	END;
END errmsg;

END locate2a;
loc2b.plm
locate2: DO;
$include(loc2b.ipx)

DECLARE aUnsatisfiedExt(*) BYTE PUBLIC INITIAL('UNSATISFIED EXTERNAL(XXXXX) '),

	aReferenceToUns(*) BYTE PUBLIC INITIAL('REFERENCE TO UNSATISFIED EXTERNAL(XXXXX) AT XXXXH', 0DH, 0AH),
	aSymbolTableOfM(*) BYTE PUBLIC INITIAL(0DH, 0AH, 'SYMBOL TABLE OF MODULE '),
	aReadFromFile(*) BYTE PUBLIC INITIAL(0DH, 0AH, 'READ FROM FILE '),
	aWrittenToFile(*) BYTE PUBLIC INITIAL(0DH, 0AH, 'WRITTEN TO FILE '),
	aValueType(*) BYTE PUBLIC INITIAL('VALUE TYPE '),
	aSymbol(*) BYTE PUBLIC INITIAL(6, 'SYMBOL'),
	aPub(*) BYTE PUBLIC INITIAL('XXXXH PUB  '),
	aSym(*) BYTE PUBLIC INITIAL('XXXXH SYM  '),
	alin(*) BYTE PUBLIC INITIAL('XXXXH LIN  '),
	aMod(*) BYTE PUBLIC INITIAL('      MOD  '),
	x5(*) BYTE PUBLIC INITIAL(5, 'XXXXX'),
	spc32(*) BYTE PUBLIC INITIAL('                                ');

DECLARE	loHiBoth BYTE PUBLIC,
	outSegType BYTE PUBLIC,
	unused(2) BYTE,
	curColumn ADDRESS PUBLIC,
	workingSegBase ADDRESS PUBLIC,
	unsatisfiedCnt ADDRESS PUBLIC INITIAL(0),
	curcol BYTE PUBLIC INITIAL(0),
	curListField ADDRESS PUBLIC INITIAL(0),
	nameLen BYTE PUBLIC INITIAL(0),
	lsout$p ADDRESS PUBLIC,
	lsout BASED lsout$p STRUCTURE (rectyp BYTE, reclen ADDRESS, record(1) BYTE);


seekOutFile: PROCEDURE(mode, pblk, pbyt) PUBLIC;
	DECLARE (mode, pblk, pbyt) ADDRESS;
	CALL seek(outputfd, mode, pblk, pbyt, .statusIO);
	CALL errChkReport(statusIO, .outFileName(1), TRUE);
END seekOutFile;


flushOut: PROCEDURE PUBLIC;
	CALL write(outputfd, outRecord$p, out$p - outRecord$p, .statusIO);
	CALL errChkReport(statusIO, .outFileName(1), TRUE);
	out$p = outRecord$p;
END flushOut;

writeBytes: PROCEDURE(buf$p, cnt) PUBLIC;
	DECLARE (buf$p, cnt) ADDRESS;
	DECLARE bcnt ADDRESS;

	bcnt = eout$p - out$p;
	DO WHILE cnt > bcnt;
		CALL mem$mov(bcnt, buf$p, out$p);
		cnt = cnt - bcnt;
		buf$p = buf$p + bcnt;
		out$p = out$p + bcnt;
		CALL flushOut;
		bcnt = npbuf;
	END;
	CALL mem$mov(cnt, buf$p, out$p);
	out$p = out$p + cnt;
END writeBytes;


initRecOut: PROCEDURE(rectyp) PUBLIC;
	DECLARE rectyp BYTE;
	DECLARE lsout BASED lsout$p STRUCTURE(rectyp BYTE, reclen ADDRESS);

	IF eout$p - out$p < 1028 THEN
		CALL flushout;
	lsout$p = out$p;
	lsout.rectyp = rectyp;
	out$p = out$p + size(lsout);	/* rectyp + length */
END initRecOut;


endRecord: PROCEDURE PUBLIC;
	DECLARE crc BYTE;
	DECLARE pch ADDRESS;
	DECLARE ch BASED pch BYTE;
	DECLARE outch BASED out$p BYTE;

	/* check record not too long */
	IF (lsout.reclen := out$p - .lsout.rectyp - 2) > 1025 THEN
		CALL errChkReport(ERR211, .outFileName(1), TRUE);	/* RECORD TOO LONG */
	/* for what is there generate the CRC */
	crc = 0;
	DO pch = .lsout TO out$p - 1;
		crc = crc + ch;
	END;
	outch = -crc;
	out$p = out$p + 1;
END endRecord;


emitMODDAT: PROCEDURE(curDataFrag$p) PUBLIC;
	DECLARE curDataFrag$p ADDRESS;
	DECLARE curDataFrag BASED curDataFrag$p DATAFRAG$T;
	DECLARE (len, bcnt, pmem, curPC, j) ADDRESS;
	DECLARE crc BYTE;
	DECLARE mem BASED pmem (1) BYTE;
	DECLARE	moddatOut BASED out$p STRUCTURE (segid BYTE, offset ADDRESS, dat(1) BYTE);

	CALL initRecOut(R$MODDAT);
	len = curDataFrag.eaddr - curDataFrag.saddr + 1;
	lsout.reclen = len + 4;
	moddatOut.segid = SABS;		/* abs seg */
	curPC = (moddatOut.offset := curDataFrag.saddr);		/* load address */	
					/* initialise the CRC */
	crc = HIGH(lsout.reclen) + 6 + LOW(lsout.reclen) + HIGH(moddatOut.offset) + LOW(moddatOut.offset);
	out$p = out$p + 3;

	DO WHILE len > 0;
		bcnt = 256;	/* write 256, or less if no page boundary or less bytes available */
		IF LOW(curPC) <> 0 THEN
			bcnt = 256 - LOW(curPC);
		IF bcnt > len THEN
			bcnt = len;
		pmem =  addrInCache(curPC);	/* get a pointer to where the data is now */
		DO j = 0 TO bcnt - 1;		/* include the data in the CRC check */
			crc = crc + mem(j);
		END;
		CALL writeBytes(pmem, bcnt);	/* add the bytes to the output record */
		IF bcnt = 256 THEN		/* if was a whole page then free it */
		DO;
			pageTab1(pageTab2(HIGH(curPC)).pageIdx).state = 0FFH;
			pageTab2(HIGH(curPC)).pageIdx = 0FFH;	/* mark page as free */
		END;
		curPC = curPC + bcnt;		/* account for written data */
		len = len - bcnt;
	END;	/* DO WHILE */

	crc = -crc;			/* close off the record */
	CALL writeBytes(.crc, 1);
END emitMODDAT;


forceSOL: PROCEDURE PUBLIC;
	IF curcol <> 0 THEN
		CALL printcrlf;
	curcol = 0;
END forceSOL;



printColumn: PROCEDURE(field, pstr) PUBLIC;
	DECLARE field ADDRESS;
	DECLARE pstr ADDRESS;
	DECLARE str BASED pstr (1) BYTE;

	/* change of field type then force new line */
	IF field <> curListField THEN
		CALL forceSOL;
	curListField = field;
	/* reached max of columns then force new line */
	IF curcol = columns THEN
		CALL forceSOL;
	/* space to next column: 32 chars - length of previous name */
	IF curcol <> 0 THEN
		CALL printString(.spc32, 32 - nameLen);
	curcol = curcol + 1;
	nameLen = str(0);
	/* print out field lead */
	CALL printString(field, 11);
	/* and the name */
	CALL printString(.str(1), nameLen);
END printColumn;


setWorkingSeg: PROCEDURE(seg) BYTE PUBLIC;	/* always returns 0 */
	DECLARE seg BYTE;
	workingSegBase = segBases(seg);
	RETURN SABS;
END setWorkingSeg;


procMODEND: PROCEDURE PUBLIC;
	DECLARE rst0$p ADDRESS;
	DECLARE jmp BASED rst0$p STRUCTURE (inst BYTE, addr ADDRESS);
	DECLARE	inModend BASED in$p STRUCTURE (modtype BYTE, segid BYTE, offset ADDRESS);
	DECLARE	outModend BASED out$p STRUCTURE (modtype BYTE, segid BYTE, offset ADDRESS);

	DECLARE curDataFrag BASED curDataFrag$p DATAFRAG$T;	/* used for size below */

	IF inModend.modtype = 1 THEN	/* is main module */
	DO;
		isMain = 1;	/* flag as main module */
		outSegType = setWorkingSeg(inModend.segid);
		IF NOT startSeen THEN	/* use the main module's address */
			startAddr = workingSegBase + inModend.offset;
		IF restart0Seen THEN	/* restart0 the put jump to startAddr */
		DO;
			rst0$p = addrInCache(0);	/* location of address 0 in cache */
			jmp.inst = 0C3H;		/* insert the jump */
			jmp.addr = startAddr;
			CALL addSegFrag(AABS, SABS, 0, 3);/* add to the frags */
			CALL addDataFrag(0, 2);
		END;
	END;
	ELSE
		startAddr = 0;	/* 0 start address if not a main module */
	CALL flushOut;			/* flush any data in the buffers */
	curDataFrag$p = topDataFrags;
	DO WHILE curDataFrag$p <> botHeap;	/* emit all of the data frags */
		curDataFrag$p = curDataFrag$p - size(curDataFrag);
		CALL emitMODDAT(curDataFrag$p);
	END;

	CALL initRecOut(R$MODEND);	/* generate the rectyp 4 record */
	outModend.modtype = isMain;
	outModend.segid = 0;
	outModend.offset = startAddr;
	out$p = out$p + 4;
	CALL writeBytes(in$p + DOUBLE(4), recLen - DOUBLE(4) - 1);	/* copy rectyp 4 tail over */
	CALL endRecord;					/* insert length and CRC */
	CALL initRecOut(R$MODEOF);				/* EOF record */
	CALL endRecord;
	CALL flushOut;
END procMODEND;


procMODDAT: PROCEDURE PUBLIC;
	DECLARE (mark, endrec) STRUCTURE (blk ADDRESS, byt ADDRESS);
	DECLARE fixextUsed BYTE;
	DECLARE (fixRAddr, pad) ADDRESS;
	DECLARE (locRecLen, recSegBaseAddr) ADDRESS;
	DECLARE (fixLoc, fixLoc2) ADDRESS;
	DECLARE (loByt, hiByt) BYTE, bothByt ADDRESS AT (.loByt);	/* used to handle cross page fixup */

	DECLARE bfix BASED fixLoc BYTE;		/* fixup location of byte */
	DECLARE bfix2 BASED fixLoc2 BYTE;	/* fixup location of 2nd byte if cross page boundary */
	DECLARE wfix BASED fixLoc ADDRESS;
	DECLARE erec BASED erec$p(1) BYTE;
	DECLARE	moddatRec BASED in$p STRUCTURE (segid BYTE, offset ADDRESS, dat(1) BYTE);
	DECLARE	inLoHiBoth BASED in$p BYTE;
	DECLARE	outLoHiBoth BASED out$p BYTE;
	DECLARE	inExtfix BASED in$p STRUCTURE (namidx ADDRESS, offset ADDRESS);
	DECLARE	outExtfix BASED out$p STRUCTURE (namidx ADDRESS, offset ADDRESS);
	DECLARE	inFixOffset BASED in$p ADDRESS;
	DECLARE	inSegId BASED in$p BYTE;

	inRecord$p = .(R$MODDAT);		/* initialise to appear as a MODDAT record */
	ibuf$p = in$p;
	CALL chkRead(3);		/* make sure the record and length are in the buffer */
	in$p = ibuf$p;
	IF moddatRec.segid = SSTACK THEN
		CALL fatalErr(ERR238);	/* ILLEGAL STACK CONTENT RECORD */

	outSegType = setWorkingSeg(moddatRec.segid);	/* set to ABS and get working base address */
	recSegBaseAddr = workingSegBase;
	/* set the bounds for this moddat record checking for wrap round */
	inFragment.saddr = recSegBaseAddr + moddatRec.offset;
	IF (inFragment.eaddr := inFragment.saddr + recLen - 5) < inFragment.saddr THEN
		CALL illegalRecord;	/* oops over 64k */
	IF moddatRec.segid = SABS THEN	/* add this as a new frag */
		CALL addSegFrag(0, 0, inFragment.saddr, recLen - 4);
	ELSE IF (segFlags(moddatRec.segid) AND AMASK) = AUNKNOWN THEN
		CALL illegalRecord;	/* align is invalid */
	IF (locRecLen := recLen) > 1025 THEN	/* long records only ok if ABS */
	DO;
		IF moddatRec.segid <> 0 THEN
			CALL fatalErr(ERR211);	/* RECORD TOO LONG */
		CALL addDataFrag(inFragment.saddr, inFragment.eaddr);
	END;
	CALL loadMODDAT(moddatRec.segid);	/* suck the data in */
	CALL getRecord;
	IF locRecLen > 1025 THEN	/* can only happen for ABS blocks so no fixup */
		RETURN;

	fixextUsed = 0;

	DO WHILE inRecord.rectyp = R$FIXEXT OR inRecord.rectyp = R$FIXLOC OR inRecord.rectyp = R$FIXSEG;
		IF inRecord.rectyp = R$FIXEXT THEN
		DO;
			IF NOT fixextUsed THEN
			DO;
				CALL flushOut;	/* make sure written to disk and record current location */
				CALL seekOutFile(SEEKTELL, .mark.blk, .mark.byt);
				endrec.blk = 0;
				endrec.byt = locRecLen + 3;
				CALL seekOutFile(SEEKFWD, .endrec.blk, .endrec.byt);	/* past where data will go */
			END;
			fixextUsed = TRUE;	/* note we have fixext record(s) */
			CALL initRecOut(R$FIXEXT);	/* initialise the fixext record */
			IF (loHiBoth := inLoHiBoth) - 1 > FBOTH - 1 THEN	/* check fixup type valid */
				CALL illegalRecord;

			outLoHiBoth = loHiBoth;	/* copy to the output */
			out$p = out$p + 1;
			in$p = in$p + 1;

			DO WHILE in$p < .erec;
				/* set the new offset but do bounds check also */
				CALL fixupBoundsChk(outExtfix.offset := recSegBaseAddr + inExtfix.offset);
				IF loHiBoth = FBOTH THEN
					CALL fixupBoundsChk(outExtfix.offset + 1);	/* check 2nd byte in range */
				IF (outExtfix.namidx := inExtfix.namidx) >= unsatisfiedCnt THEN
					CALL badRecordSeq;	/* name index out of range */
				CALL forceSOL;			/* make sure on new line to record the problem */
				CALL binasc(outExtfix.namidx, 10, ' ', .aReferenceToUns(34), 5);
				CALL binasc(outExtfix.offset, 16, '0', .aReferenceToUns(44), 4);
				/* print 'REFERENCE TO UNSATISFIED EXTERNAL(' */
				CALL conAndPrint(.aReferenceToUns, 34);
				/* print the 'XXXXX) AT XXXXH\r\n' skipping leading spaces */
				CALL conAndPrint((curColumn := skipspc(.aReferenceToUns(34))),
						17 - (curColumn - .aReferenceToUns(34)));
				out$p = out$p + 4;	/* advance out and in position */
				in$p = in$p + 4;
			END;	/* DO WHILE */

			CALL endRecord;	/* finish off the record */
		END;
		ELSE
		DO;
			workingSegBase = recSegBaseAddr;			/* restore fixup seg base to the moddat's own */
			IF inRecord.rectyp = R$FIXSEG THEN				/* if different seg the set it up */
			DO;
				outSegType = setWorkingSeg(inSegid);
				in$p = in$p + 1;				/* rest treat as rectyp FIXLOC */
			END;
			IF (loHiBoth := inLoHiBoth) - 1 > FBOTH - 1 THEN	/* is fixup valid */
				CALL illegalRecord;
			in$p = in$p + 1;

			DO WHILE in$p < .erec;					/* process the record */
				/* set the fixup real address and check in range, both bytes for BOTH fixup type */
				CALL fixupBoundsChk(fixRaddr := recSegBaseAddr + inFixOffset);
				IF loHiBoth = FBOTH THEN
					CALL fixupBoundsChk(fixRaddr + 1);
				/* find the fixup location in memory */
				fixLoc = addrInCache(fixRaddr);
				DO CASE loHiBoth - 1;
					bfix = bfix + LOW(workingSegBase);	/* LO add in low of target base address */
					bfix = bfix + HIGH(workingSegBase);	/* HI add in high of target base address */
										/* BOTH */
					IF LOW(fixRaddr) <> 0FFH THEN	/* doesn't span pages */
						wfix = wfix + workingSegBase;	/* simple add in to existing word value */
					ELSE
					DO;
						lobyt = bfix;			/* get the low byte */
						fixLoc2 = addrInCache(fixRaddr + 1);	/* find address of high byte */
						hibyt = bfix2;
						bothbyt = bothbyt + workingSegBase;	/* add in the target base address */
						bfix = lobyt;			/* and put back in place */
						bfix2 = hibyt;
					END;
				END;
				in$p = in$p + 2;
			END;
		   END;
		CALL getRecord;
	END;
	IF fixextUsed THEN
	DO;
		CALL flushOut;	/* flush the extdef data */
		CALL seekOutFile(SEEKABS, .mark.blk, .mark.byt);	/* backup to the data record */
		CALL emitMODDAT(.inFragment.saddr);			/* emit the data record fragment as it needs fixup */
		CALL flushOut;
		CALL seekOutFile(SEEKEND, .endrec.blk, .endrec.byt);	/* back to end of file */
	END;
	ELSE
		CALL addDataFrag(inFragment.saddr, inFragment.eaddr);	/* record the fragment */

END procMODDAT;

END locate2;

loc3.plm
locate3: DO;
$include(loc3.ipx)

fixupBoundsChk: PROCEDURE(addr) PUBLIC;
	DECLARE addr ADDRESS;
	IF addr < inFragment.saddr OR inFragment.eaddr < addr THEN
		CALL fatalErr(ERR213);	/* Fixup Bounds Error */
END fixupBoundsChk;


procLINNUM: PROCEDURE PUBLIC;
	DECLARE	inSeg BASED in$p BYTE;
	DECLARE	inLinDef BASED in$p STRUCTURE (offset ADDRESS, linnum ADDRESS);
	DECLARE	outSeg BASED out$p BYTE;
	DECLARE	outLinDef BASED out$p STRUCTURE (offset ADDRESS, linnum ADDRESS);

	IF NOT PurgeSeen OR LinesSeen THEN	/* only process if not purge or lines requested */
	DO;
		outSegType =  setWorkingSeg(inSeg);	/* get ABS seg and set working base address */
		IF NOT PurgeSeen THEN		/* need to create output record */
		DO;
			CALL initRecOut(R$LINNUM);
			outSeg = outSegType;
			out$p = out$p + 1;
		END;
		in$p = in$p + 1;		/* past segid */
		DO WHILE in$p < .erec;		/* loop over all entries */
			IF NOT PurgeSeen THEN	/* emit to output record */
			DO;
				outLinDef.offset = workingSegBase + inLinDef.offset;	/* final location */
				outLinDef.linnum = inLinDef.linnum;	/* copy the line number */
				out$p = out$p + size(outLinDef);	/* past this entry */
				
			END;
			IF LinesSeen THEN	/* if required list the mapping */
			DO;
				CALL binasc(workingSegBase + inLinDef.offset, 16, '0', .alin, 4);
				CALL binasc(inLinDef.linnum, 10, ' ', .x5(1), 5);
				CALL printColumn(.alin, .x5);
			END;
			in$p = in$p + size(inLinDef);			/* to next entry */
		END;
		IF NOT PurgeSeen THEN		/* finish off output record */
			CALL endRecord;
	END;
	CALL getRecord;	/* prep next record */
END procLINNUM;


procANCEST: PROCEDURE PUBLIC;
	DECLARE str BASED in$p (1) BYTE;
	IF LinesSeen OR SymbolsSeen THEN	/* if lines or symbols */
	DO;
		CALL printColumn(.aMod, in$p);	/* print the ancestor */
		CALL forceSOL;
	END;
	IF NOT PurgeSeen THEN			/* if needed in output */
	DO;
		CALL initRecOut(R$ANCEST);	/* init record, copy the name and finalise */
		CALL writeBytes(.str(0), str(0) + 1);
		CALL endRecord;
	END;
	CALL getRecord;	/* prep next record */
END procANCEST;


procDefs: PROCEDURE(list, template) PUBLIC;
	DECLARE list BYTE;
	DECLARE template ADDRESS;
	DECLARE ritem BASED in$p STRUCTURE (len ADDRESS, name (1) BYTE);
	DECLARE owd BASED out$p ADDRESS;
	DECLARE buf BASED out$p (1) BYTE;
	DECLARE inSeg BASED in$p BYTE;
	DECLARE inDef BASED in$p STRUCTURE (offset ADDRESS, name(1) BYTE /*, 0 */);
	DECLARE outSeg BASED out$p BYTE;
	DECLARE outDef BASED out$p STRUCTURE (offset ADDRESS, name(1) BYTE /*, 0 */);

	IF NOT purgeseen OR list THEN	/* only process if not purged or list requested for these symbols */
	DO;
		IF inSeg = SSTACK THEN	/* stack ? */
		DO;
			outSegType = SSTACK;
			workingSegBase = 0;			/* stack base is 0 */
		END;
		ELSE
			outSegType = setWorkingSeg(inSeg);	/* sets working base address * returns ABS Seg */
		IF NOT PurgeSeen THEN
		DO;
			CALL initRecOut(inRecord.rectyp);	/* initialise the output record */
			outSeg = outSegType;
			out$p = out$p + 1;
		END;
		in$p = in$p + 1;			/* past segId */
		DO WHILE in$p < .erec;
			IF NOT PurgeSeen THEN
			DO;
				outDef.offset = workingSegBase + inDef.offset;	/* add in the base address */
				CALL pstrcpy(.inDef.name, .outDef.name);	/* copy the name over */
				outDef.name(inDef.name(0) + 1) = 0;		/* add the trailing 0 */
				out$p = out$p + 4 + inDef.name(0);		/* update output record past offset, name and extra 0 */
			END;
			IF list THEN			/* if required list the location */
			DO;
				CALL binasc(workingSegBase + inDef.offset, 16, '0', template, 4);
				CALL printColumn(template, .inDef.name);
			END;
			in$p = in$p + 4 + inDef.name(0);			/* to the next entry - past offset, name and extra 0 */
		END;
		IF NOT PurgeSeen THEN		/* if not purged finish off the output record */
			CALL endRecord;
	END;
	CALL getRecord;	/* prep for next record */
END procDefs;


procEXTNAM: PROCEDURE PUBLIC;
	DECLARE ch BASED in$p BYTE;
	DECLARE buf BASED out$p (1) BYTE;
	DECLARE inName BASED in$p STRUCTURE (len BYTE, name(1) BYTE /*, 0 */);
	DECLARE outName BASED out$p STRUCTURE (len BYTE, name(1) BYTE /*, 0 */);

	CALL initRecOut(R$EXTNAM);	/* initialise the EXTNAM in the located file */
	DO WHILE in$p < .erec;		/* process each item in the input EXTNAM record */
		CALL forceSOL;
		/* insert the index number of the unsatisfied external & print the error message */
		CALL binasc((unsatisfiedCnt := unsatisfiedCnt + 1) - 1, 10, ' ', .aUnsatisfiedExt(21), 5);
		/* print to the start of the number */
		CALL conAndPrint(.aUnsatisfiedExt, 21);
		/* print the rest of the message omitting leading spaces in the number */
		CALL conAndPrint(curColumn := skipSpc(.aUnsatisfiedExt(21)), 7 - (curColumn - .aUnsatisfiedExt(21)));
		/* print the name */
		CALL conAndPrint(.inName.name, inName.len);
		CALL conAndPrint(.crlf, 2);
		/* copy the name to the output record */
		CALL pstrcpy(.inName, .outName);
		outName.name(inName.len) = 0;	/* add the zero */
		out$p = out$p + 2 + inName.len;			/* skip past the name in out and in records */
		in$p = in$p + 2 + inName.len;
	END;
	CALL endRecord;	/* finish off the record */
	CALL getRecord; /* prep the next record */
END procEXTNAM;


printListingHeader: PROCEDURE(buf, len) PUBLIC;
	DECLARE buf ADDRESS;
	DECLARE len ADDRESS;
	CALL printString(buf, len);
	CALL printString(.moduleName(1), moduleName(0));
	CALL printString(.aReadFromFile, 17);	/* '\r\nREAD FROM FILE ' */
	CALL printString(.inFileName(1), inFileName(0));
	CALL printString(.aWrittenToFile, 18); /* '\r\nWRITTEN TO FILE ' */
	CALL printString(.outFileName(1), outFileName(0));
	CALL printcrlf;
END printListingHeader;


/* main procedure to locate the file */
locateFile: PROCEDURE PUBLIC;
	DECLARE buf BASED out$p (1) BYTE;
	CALL procHdrAndComDef;		/* determine all of the segment base addresses */
	CALL initRecOut(R$MODHDR);	/* create header record */
	CALL writeBytes(.moduleName, moduleName(0)+1);	/* write the name */
	buf(0) = modhdr$x1;	/* copy the passed in x field data */
	buf(1) = modhdr$x2;
	out$p = out$p + 2;
	CALL endRecord;		/* located modhdrs don't have seg size info */
	/* print symbol table header message if required */
	IF PublicsSeen  OR SymbolsSeen OR LinesSeen THEN
	DO;
		CALL printListingHeader(.aSymbolTableOfM, 25);
		CALL printcrlf;
		/* print the column headings */
		DO curColumn = 1 TO columns;
			CALL printColumn(.aValueType, .aSymbol);
		END;
		CALL printcrlf;
	END;

	/* process the link file */
	DO WHILE inRecord.rectyp <> R$MODEND;
	    DO CASE SHR(inRecord.rectyp, 1);
		CALL illegalReloc;	/* 0 */
		CALL badRecordSeq;	/* 2 - modhdr already processed */
		;			/* 4 - modend exited while already */
		CALL procMODDAT;		/* 6 - moddat */
		CALL procLINNUM;		/* 8 - linnum */
		CALL illegalReloc;	/* 0A */
		CALL illegalReloc;	/* 0C */
		CALL errChkReport(ERR204, .inFileName(1), TRUE); /* 0E - modeof Premature EOF */
		CALL procANCEST;		/* 10 - ancest */
		CALL procDefs(SymbolsSeen, .aSym);	/* 12 - locdef */
		CALL illegalReloc;	/* 14 */
		CALL procDefs(PublicsSeen, .aPub);	/* 16 - pubdef */
		call procEXTNAM;		/* 18 - extnam - externals */
		CALL illegalReloc;	/* 1A */
		CALL illegalReloc;	/* 1C */
		CALL illegalReloc;	/* 1E */
		CALL badRecordSeq;	/* 20 - fixext link should have processed these */
		CALL badRecordSeq;	/* 22 - fixloc link should have processed these */
		CALL badRecordSeq;	/* 24 - fixseg link should have processed these */
		CALL badRecordSeq;	/* 26 - libloc attempting to locate a library */
		CALL badRecordSeq;	/* 28 - libnam attempting to locate a library */
		CALL badRecordSeq;	/* 2A - libdic attempting to locate a library */
		CALL badRecordSeq;	/* 2C - libhdr attempting to locate a library */
		CALL badRecordSeq;	/* 2E - processed comdef already */
	    END;
	END;
	CALL procMODEND;		/* read the modend and generate the rest of the located file */
	CALL close(outputfd, .statusIO);
	CALL errChkReport(statusIO, .outFileName(1), TRUE);
	IF havePagingFile THEN		/* get rid of any paging file */
	DO;
		CALL close(tmpfd, .statusIO);
		CALL delete(.tmpFileName(1), .statusIO);
	END;
	CALL forceSOL;		/* make sure at start of line if symbols listed */
	CALL printMemoryMap;	/* print the final memory map + any overlay errors */
	CALL close(readfd, .statusIO);
	CALL errChkReport(statusIO, .inFileName(1), TRUE);
END locateFile;

END locate3;
loc4.plm
locate4: DO;
$include(loc4.ipx)

DECLARE	aMemoryMapOfMod(*) BYTE PUBLIC INITIAL(0DH, 0AH, 'MEMORY MAP OF MODULE '),
	aStartStopLengt(*) BYTE PUBLIC INITIAL(0DH, 0AH, 0AH, 'START   STOP LENGTH REL NAME', 0DH, 0AH, 0AH),
	aModuleStartAdd(*) BYTE PUBLIC INITIAL('MODULE START ADDRESS XXXXH'),
	aModuleIsNotAMa(*) BYTE PUBLIC INITIAL('MODULE IS NOT A MAIN MODULE'),
	aStartControlIg(*) BYTE PUBLIC INITIAL(', START CONTROL IGNORED'),
	aRestartControl(*) BYTE PUBLIC INITIAL(', RESTART CONTROL IGNORED'),
	aAddresses(*) BYTE PUBLIC INITIAL('XXXXH  XXXXH  XXXXH  X  '),
	aMemOverlap(*) BYTE PUBLIC INITIAL('  (MEMORY OVERLAP FROM XXXXH THROUGH XXXXH)'),
	a0LengthSegment(*) BYTE PUBLIC INITIAL('  (0 LENGTH SEGMENT WRAPPED AROUND TO 0000H)'),
	segNames(*) BYTE PUBLIC INITIAL('ABSOLUTE', 'CODE    ', 'DATA    ', 'STACK   ', 'MEMORY  '),
	alignNames(*) BYTE PUBLIC INITIAL('AIPB');

getCommonName: PROCEDURE(segid) ADDRESS PUBLIC;
	DECLARE segid BYTE;
	DECLARE cdef BASED in$p STRUCTURE (segid BYTE, name(1) BYTE);

	IF segid = SBLANK THEN
		RETURN .(0);	/* blank name */
	CALL objSeek(0,0);	/* seek start of file */
	CALL getRecord;		/* junk the modhdr record */
	DO WHILE 1;
		CALL getRecord;	/* scan comdef records */
		IF inRecord.rectyp <> R$COMDEF THEN
			CALL badRecordSeq;
		DO WHILE in$p < .erec;	/* look for the seg */
			IF segid = cdef.segid THEN
				RETURN .cdef.name(0);	/* return pointer to name */
			in$p = in$p + size(cdef) + cdef.name(0);	/* skip to next seg/name */
		END;
	END;
END getCommonName;

printMemoryMap: PROCEDURE PUBLIC;
	DECLARE (minStart, q) ADDRESS;
	DECLARE (i, wrapped, addCRLF) BYTE;
	DECLARE curSegFrag BASED curSegFrag$p SEGFRAG$T;
	DECLARE omfName BASED in$p STRUCTURE(len BYTE, name(1) BYTE);

	IF MapSeen THEN		/* print map header info if asked for */
	DO;
		CALL printListingHeader(.aMemoryMapOfMod, size(aMemoryMapOfMod)); /* '\r\nMEMORY MAP OF MODULE ' */
		IF isMain THEN
		DO;
			CALL binasc(startAddr, 16, '0', .aModuleStartAdd(21), 4);  /* insert start address in string */
			CALL printString(.aModuleStartAdd, size(aModuleStartAdd)); /* 'MODULE START ADDRESS XXXXH' */
		END;
		ELSE
		DO;
			CALL printString(.aModuleIsNotAMa, size(aModuleIsNotAMa)); /* 'MODULE IS NOT A MAIN MODULE' */
			IF startSeen THEN
				CALL printString(.aStartControlIg, size(aStartControLIg)); /* ', START CONTROL IGNORED' */
			IF restart0Seen THEN
				CALL printString(.aRestartControl, size(aRestartControl)); /* ', RESTART CONTROL IGNORED' */
		END;
		CALL printString(.aStartStopLengt, 34);	/* '\r\nSTART   STOP LENGTH REL NAME\r\n\n' */
	END;

	segBases(SSTACK) = segBases(SSTACK) - segSizes(SSTACK);
	DO q = 1 TO 254;
		IF segSizes(i := segOrder(q)) > SABS  OR (segFlags(i) AND 20H) <> 0 THEN
			CALL addSegFrag(segFlags(i), i, segBases(i), segSizes(i));
	END;

	minStart, wrapped = 0;
	/* emit information on all of the blocks */
	curSegFrag$p = topHeap;
	DO WHILE curSegFrag$p <> topDataFrags; 	/* done ? */
		curSegFrag$p = curSegFrag$p - size(curSegFrag);	/* point to start of block info */
		CALL binasc(curSegFrag.start, 16, '0', .aAddresses, 4);	/* start address */
		aAddresses(7) = '*';		/* assume no length so show **** for stop address */
		aAddresses(8) = '*';
		aAddresses(9) = '*';
		aAddresses(10) = '*';
		IF curSegFrag.len > 0 THEN		/* we have length so fill stop address */
			CALL binasc(curSegFrag.start + curSegFrag.len - 1, 16, '0', .aAddresses(7), 4);
		CALL binasc(curSegFrag.len, 16, ' ', .aAddresses(14), 4);	/* length */
		/* copy the start and stop into the memory overlap message */
		CALL mem$mov(4, .aAddresses, .aMemOverlap(23));
		CALL mem$mov(4, .aAddresses(7), .aMemOverlap(37));
		IF curSegFrag.start + curSegFrag.len > minStart THEN	/* none or not total overlap */
		DO;
			aMemOverlap(37) = '*';		/* clear out the through address */
			aMemOverlap(38) = '*';
			aMemOverlap(39) = '*';
			aMemOverlap(40) = '*';
			/* if we are overlap put in the end of the last block */
			IF curSegFrag.len > 0 AND (minStart > 0  OR wrapped <> 0) THEN
				CALL binasc(minStart - 1, 16, '0', .aMemOverlap(37), 4);
		END;
		addCRLF = 0;
		IF mapSeen THEN	/* if MAP is being produced */
		DO;
			aAddresses(21) = alignNames(curSegFrag.flags AND AMASK);	/* add the align type */
			CALL printString(.aAddresses, size(aAddresses));	/* and print the address info */
			IF curSegFrag.seg >= SNAMED THEN				/* seg is named common */
			DO;
				CALL printString(.('/'), 1);			/* print the / */
				in$p = getCommonName(curSegFrag.seg);		/* look up the common name in the input file */
				CALL printString(.omfName.name, omfName.len);	/* print it and the closing / */
				CALL printString(.('/'), 1);
			END;
			ELSE							/* standard so used the canned names */
				CALL printString(.segNames(SHL(curSegFrag.seg, 3)), 8);	/* shl 3 is *8 */
		END;
		IF curSegFrag.start < minStart OR wrapped <> 0 THEN			/* emit any overlap notification */
		DO;
			addCRLF = TRUE;
			/* '  (MEMORY OVERLAP FROM XXXXH THROUGH XXXXH)'  values filled in above */
			CALL conAndPrint(.aMemOverlap, size(aMemOverlap));
		END;
		IF (curSegFrag.flags AND FWRAP0) <> 0 THEN				/* and if wrapped to 0 */
		DO;
			addCRLF = TRUE;
			/* '  (0 LENGTH SEGMENT WRAPPED AROUND TO 0000H)' */
			CALL conAndPrint(.a0LengthSegment, size(a0LengthSegment));
		END;
		IF addCRLF THEN							/* finish of any started line */
			CALL conAndPrint(.crlf, 2);
		ELSE IF MapSeen THEN
			CALL printcrlf;
		IF curSegFrag.start + curSegFrag.len = 0 AND curSegFrag.len > 0 THEN		/* we wrapped to 0 */
		DO;
			minStart = 0;
			wrapped = FWRAP0;
		END;
		ELSE IF wrapped = 0 THEN
			IF curSegFrag.start + curSegFrag.len > minStart THEN		/* update the minStart */
				minStart = curSegFrag.start + curSegFrag.len;
	END;	/* DO WHILE */
END printMemoryMap;






skipspc: PROCEDURE(pch) ADDRESS PUBLIC;
	DECLARE pch ADDRESS;
	DECLARE ch BASED pch BYTE;
	
	DO WHILE ch = ' ';
		pch = pch + 1;
	END;
	RETURN pch;
END skipspc;




END locate4;
loc4a.plm
loc4a: DO;
$include(loc4a.ipx)

errChkReport: PROCEDURE(errCode, file, errExit) PUBLIC;
	DECLARE errCode ADDRESS;
	DECLARE file ADDRESS;
	DECLARE errExit BYTE;
	DECLARE status ADDRESS;
	
	IF errCode <> 0 THEN
	DO;
		file = skipspc(file);
		CALL write(0, .(' '), 1, .status);
		CALL write(0, file, pastAFN(file) - file, .status);	/* write file name */
		CALL write(0, .(','), 1, .status);
		CALL errmsg(errCode);
		IF errExit THEN
			CALL exit;
	END;
END errChkReport;
END;
loc5a.plm
locate5a: DO;
$include(loc5a.ipx)

DECLARE hexdigits(*) BYTE DATA ('0123456789ABCDEF');

binasc: PROCEDURE(number, base,	pad, bufp, ndigits) PUBLIC;
	DECLARE number ADDRESS, (base, pad) BYTE, bufp ADDRESS, ndigits BYTE;
	DECLARE i BYTE;
	DECLARE buf BASED bufp (1) BYTE;
	
	DO i = 1 TO ndigits;
		buf(ndigits - i) = hexdigits(number MOD base);
		number = number / base;
	END;

	i = 0;
	DO WHILE buf(i) = '0' AND i < ndigits - 1;
		buf(i) = pad;
		i = i + 1;
	END;
END binasc;


pastAFN: PROCEDURE(pch) ADDRESS PUBLIC;
	DECLARE pch ADDRESS;
	DECLARE ch BASED pch BYTE;

	DO WHILE 1;
		pch = pastFileName(pch);
		IF ch = '*' OR ch = '?' THEN
			pch = pch + 1;
		ELSE
			RETURN pch;
	END;
END pastAFN;

END locate5a;


loc5b.plm
locate5b: DO;

pastFileName: PROCEDURE(pch) ADDRESS PUBLIC;
	DECLARE pch ADDRESS;
	DECLARE ch BASED pch BYTE;

	DO WHILE ch = ':' OR ch = '.' OR ('0' <= ch AND ch <= '9') OR ('A' <= ch AND ch <= 'Z');
		pch = pch + 1;
	END;
	RETURN pch;
END pastFileName;


END locate5b;


loc6.plm
locate6: DO;
$include(loc6.ipx)



DECLARE spathInfo SPATH$T PUBLIC;
DECLARE signonMsg(*) BYTE PUBLIC INITIAL(0CH, 'ISIS-II OBJECT LOCATER '),
	aInvokedBy(*) BYTE PUBLIC INITIAL(' INVOKED BY:', 0Dh, 0Ah),
	aCommandTailErr(*) BYTE PUBLIC INITIAL('COMMAND TAIL ERROR NEAR #:'),
	tmpFileInfo(*) BYTE PUBLIC INITIAL(0, 'LOCATETMP'),
	scmd$p ADDRESS PUBLIC,
	cmd$p ADDRESS PUBLIC;
DECLARE nxtSegOrder BYTE EXTERNAL;

/* explicit declare of based variables as base defined after include */
declare	cmdch based cmd$p byte;



DECLARE controls(*) BYTE PUBLIC DATA(
		0, 0, 253,		/* 253 = -3 */
		1, 1, 4, 'CODE',
		1, 2, 4, 'DATA',
		1, 3, 5, 'STACK',
		1, 4, 6, 'MEMORY',
		2, 0, 5, 'START',
		3, 1, 9, 'STACKSIZE',
		4, 2, 8, 'RESTART0',
		4, 3, 3, 'MAP',
		4, 4, 7, 'PUBLICS',
		4, 5, 7, 'SYMBOLS',
		4, 6, 5, 'LINES',
		4, 7, 5, 'PURGE',
		5, 8, 4, 'NAME',
		6, 0, 5, 'PRINT',
		7, 0, 5, 'ORDER',
		8, 0, 7, 'COLUMNS');

DECLARE cin(*) BYTE PUBLIC DATA(':CI: ');
DECLARE cout(*) BYTE PUBLIC DATA(5,':CO: ');
DECLARE mdebug(*) BYTE PUBLIC DATA('DEBUG ');
DECLARE mstar2(*) BYTE PUBLIC DATA('**');
DECLARE mto(*) BYTE PUBLIC DATA('TO ');
DECLARE mtoand(*) BYTE PUBLIC DATA('TO&');




cmdErr: PROCEDURE(err) PUBLIC;
	DECLARE err ADDRESS;
	DECLARE scmdch BASED scmd$p BYTE;

	IF pastFileName(cmd$p) = cmd$p THEN
	DO;
		IF cmdch <> 0DH THEN
			cmd$p = cmd$p + 1;
	END;
	ELSE
		cmd$p = pastFileName(cmd$p);

	cmdch = '#';	/* put a marker in */
	CALL conStrOut(.aCommandTailErr, 26);
	CALL errmsg(err);
	CALL conStrOut(.scmdch, cmd$p - .scmdch + 1);
	CALL conStrOut(.crlf, 2);
	CALL exit;
END cmdErr;


skipNonArgChars: PROCEDURE(pch) PUBLIC;
	DECLARE pch ADDRESS;
	cmd$p = skipspc(pch);
	/* skip any continuation bits and leading space */
	DO WHILE cmdch = '&';
		cmd$p = skipspc(cmd$p + 5);
	END;
END skipNonArgChars;


expectChar: PROCEDURE(ch, err) PUBLIC;
	DECLARE (ch, err) BYTE;
	CALL skipNonArgChars(cmd$p);
	IF ch = cmdch THEN
		CALL skipNonArgChars(cmd$p + 1);
	ELSE
		CALL cmdErr(err);
END expectChar;


expectLP: PROCEDURE PUBLIC;
	CALL expectChar('(', ERR227);	/* left parenthesis expected */
END expectLP;


expectRP: PROCEDURE PUBLIC;
	CALL expectChar(')', ERR228);	/* right parenthesis expected */
END expectRP;


expectSlash: PROCEDURE PUBLIC;
	CALL expectChar('/', ERR203);	/* invalid syntax */
END expectSlash;


getFile: PROCEDURE PUBLIC;
	CALL spath(cmd$p, .spathInfo, .statusIO);	/* try to parse file */
	IF statusIO > 0 THEN
		CALL cmdERR(statusIO);
	CALL skipNonArgChars(pastFileName(cmd$p));
END getFile;


initSegOrder: PROCEDURE PUBLIC;
	DECLARE i BYTE;
	segOrder(0) = SABS;
	segOrder(1) = SCODE;
	segOrder(2) = SSTACK;
	/* next come the common segs in order */
	DO i = 3 TO 252;
		segOrder(i) = i + 3;
	END;
	/* DATA is after these and MEMORY is last */
	segOrder(253) = SDATA;
	segOrder(254) = SMEMORY;
END initSegOrder;


errNotADisk: PROCEDURE PUBLIC;
	CALL makeFullName(.spathInfo, .inFileName(1));
	CALL errChkReport(ERR17, .inFileName(1), TRUE);	/* not a disk file */
END errNotADisk;


getpstrName: PROCEDURE(pstr) PUBLIC;
	DECLARE pstr ADDRESS;
	DECLARE str BASED pstr (1) BYTE;

	IF cmdCh < '?' OR 'Z' < cmdCh THEN
		CALL cmdErr(ERR225);	/* invalid Name */
	str(0) = 0;			/* set length */
	DO WHILE '0' <= cmdCh AND cmdCh <= '9' OR '?' <= cmdCh and cmdCh <= 'Z';
		str(0) = str(0) + 1;
		IF str(0) > 31 THEN
			CALL cmdErr(ERR226);	/* name too long */
		str(str(0)) = cmdch;
		cmd$p = cmd$p + 1;
	END;
END getpstrName;


processControls: PROCEDURE PUBLIC;
	DECLARE (p, q, cwindex) ADDRESS;
	DECLARE cindex BYTE AT(.cwindex);
	DECLARE (cid, caux, clen, cSegId) BYTE;

	/* find the command length */
	clen = pastFileName(cmd$p) - cmd$p;
	cindex = 3;
	cid = 0;
	IF cmdch = '/' THEN	/* common seg address */
	DO;
		clen = 1;	/* treat specially */
		cid = 9;
	END;
	ELSE IF clen > 0 THEN	/* look up the control */
		DO WHILE cindex < length(controls);
			IF controls(cindex+2) = clen AND strequ(cmd$p, .controls(cindex+3), clen) THEN
			DO;
				cid = controls(cindex);	
				caux = controls(cindex+1);
				cindex = length(controls);
			END;
			ELSE
				cindex = cindex + controls(cindex + 2) + 3;
		END;
	IF cid = 0 THEN		/* check we have a valid command */
		CALL cmdERR(ERR229);	/* unrecognised control */
	CALL skipNonArgChars(cmd$p + clen);
	DO CASE cid - 1;
	    DO;		/* CODE, DATA, STACK, MEMORY */
		segFlags(caux) = FHASADDR;	/* note that the address is specified */
		segBases(caux) = parseLPNumRP;	/* and its value */
	    END;
	    DO;		/* START */
	    	startSeen = TRUE;		/* got a start address */
		startAddr = parseLPNumRP;	/* and its value */
	    END;
	    DO;		/* STACKSIZE */
	        stackSizeSeen = TRUE;		/* got a stack size */
		segSizes(SSTACK) = parseLPNumRP;/* and its value */
	    END;
	    seen(caux) = TRUE; 		/* simple command seen - RESTART0, MAP, PUBLICS, SYMBOLS, LINES, PURGE */
	    DO;		/* NAME */
	    	moduleNameSeen = TRUE;		/* got a module name */
		CALL expectLP;
		CALL getpstrName(.moduleName);	/* getand its value */
		CALL expectRP;
	    END;
	    DO;		/* PRINT */		/* get the print file */
		CALL expectLP;
		CALL getFile;
		CALL makeFullName(.spathInfo, .printFileName(1));
		printFileName(0) = pastFileName(.printFileName(1)) - .printFileName(1);
		CALL expectRP;
	    END;
	    DO;		/* ORDER */		/* process the order list */
	    	CALL initSegOrder;
		nxtSegOrder = 0;
		CALL expectLP;
		DO WHILE cmdch <> ')';
			cindex = 3;			/* skip seg 0 */
			DO WHILE controls(cindex) = 1;	/* CODE DATA STACK MEMORY */
				IF strequ(cmd$p, .controls(cindex + 3), controls(cindex + 2)) THEN
				DO;
					CALL insSegIdOrder(controls(cindex + 1));
					cmd$p = cmd$p + controls(cindex + 2);
					cindex = 0;
				END;
				/* note if cindex = 0 then this keeps where we are & will terminate loop */
				cindex = cindex + controls(cindex + 2) + 3;
			END;
			IF cindex <> 0 THEN	/* check we haven't already processed */
			DO;			/* if not scan for /common/ names */
				CALL expectSlash;
				cSegId = getCommonSegId;
				CALL expectSlash;
				CALL insSegIdOrder(cSegId);
			END;
			CALL skipNonArgChars(cmd$p);
			/* look for list of names */
			IF cmdch = ',' THEN
				CALL expectChar(',', ERR203); /* invalid syntax */
		END;
		CALL expectRP;
	    END;
	    DO;		/* COLUMNS */		/* get the number of columns */
	    	p = cmd$p;	/* mark where we are */
		CALL expectLP;	/* look for the ( */
		q = cmd$p;	/* mark start of number */
		cmd$p = p;	/* reset to start of (n) */
		columns = parseLPNumRP;
		IF columns < 1 OR columns > 3 THEN	/* must be 1-3 */
		DO;
			cmd$p = q;	/* for error reporting */
			CALL cmdErr(ERR203);	/* invalid syntax */
		END;
	    END;
	    DO;		/* / -> common */	/* find the common name or blank */
	    	cSegId = getCommonSegId;	/* look up seg */
		CALL expectSlash;		/* end with a slash */
		segFlags(cSegId) = FHASADDR;	/* note we have an address */
		segBases(cSegId) = parseLPNumRP; /* and its value */
	    END;
	END;
END processControls;
END locate6;
loc7.plm
locate7: DO;
$include(loc7.ipx)

DECLARE cin(1) BYTE EXTERNAL;
DECLARE cout(1) BYTE EXTERNAL;
DECLARE mdebug(1) BYTE EXTERNAL;
DECLARE mstar2(1) BYTE EXTERNAL;
DECLARE mto(1) BYTE EXTERNAL;
DECLARE mtoand(1) BYTE EXTERNAL;

DECLARE nxtSegOrder BYTE PUBLIC;

parseLPNumRP: PROCEDURE ADDRESS PUBLIC;
	DECLARE num ADDRESS;

	CALL expectLP;
	IF cmdCh < '0' OR '9' < cmdCh THEN	/* must have digit */
		CALL cmdErr(ERR203);	/* invalid syntax */
	num = parseNumber(.cmd$p);
	IF pastFileName(cmd$p) <> cmd$p THEN	/* make sure not a filename or bad number */
		CALL cmdErr(ERR203);	/* invalid syntax */
	CALL expectRP;
	RETURN num;
END parseLPNumRP;


getCommonSegId: PROCEDURE BYTE PUBLIC;
	DECLARE name(32) BYTE;
	DECLARE	comdefRec BASED in$p STRUCTURE (segid BYTE, name(1) BYTE);

	IF cmdch = '/' THEN
		RETURN SBLANK;	/* blank common */
	CALL getpstrName(.name);
	name(name(0) + 1) = ' ';	/* space at end */
	CALL objSeek(0, 0);
	CALL getRecord;		/* skip modhdr record */

	DO WHILE 1;
		CALL getRecord;
		IF inRecord.rectyp <> R$COMDEF THEN
			CALL errChkReport(ERR237, .name(1), TRUE);	/* common not found */
		DO WHILE in$p < .erec;
			IF strequ(.name, .comdefRec.name, name(0)+1) THEN
				RETURN comdefRec.segid;
			in$p = in$p + 2 + comdefRec.name(0);
		END;
	END;
END getCommonSegId;


insSegIdOrder: PROCEDURE(seg) PUBLIC;
	DECLARE seg BYTE;
	DECLARE i BYTE;
	nxtSegOrder = nxtSegOrder + 1;
	IF segOrder(nxtSegOrder) = seg THEN	/* already in order */
		RETURN;
	DO i = 0 TO nxtSegOrder;
		IF segOrder(i) = seg THEN	/* already allocated ? */
			CALL cmdErr(ERR203);	/* invalid syntax */
	END;
	DO i = nxtSegOrder + 1 TO SBLANK - 1;	/* find the current location of seg */
		IF segOrder(i) = seg THEN
			goto L6C93;
	END;
L6C93:
	DO WHILE i > nxtSegOrder;
		segOrder(i) = segOrder(i - 1);	/* make room for this seg */
		i = i - 1;
	END;
	segOrder(nxtSegOrder) = seg;		/* insert it */
END insSegIdOrder;


readCmdLine: PROCEDURE PUBLIC;

	CALL read(1, cmd$p, 128, .actRead, .statusIO);
	CALL errChkReport(statusIO, .cin, TRUE);
	cmdbuf(actRead) = 0DH;
	call strupr(cmd$p);
END readCmdLine;


procArgs$Init: PROCEDURE PUBLIC;
	DECLARE scmdch BASED scmd$p BYTE;
	DECLARE (p, args$p) ADDRESS;

	/* work out a good io buf size */
	/* checks are for >= 32k of space, >= 16k of space */
	IF (pageCacheSize := HIGH(memck - baseMemImage) - 1) >= 128 THEN
		npbuf = 4096;
	ELSE IF pageCacheSize >= 64 THEN
		npbuf = 2048;
	ELSE
		npbuf = 1056;

	eibuf$p, ibuf$p = memck;	/* in buffer at top of memory */
	sibuf$p, eout$p = eibuf$p - npbuf;	/* output buffer is below this */
						/* and for now now print buffer */
	outRecord$p, out$p, spbuf$p, pbuf$p, epbuf$p = eout$p - npbuf;
	scmd$p = .MEMORY;		/* command line buffer */
	/* if >= 32k then also use a print buffer */
	IF (usePrintBuf := pageCacheSize >= 64) THEN
		scmd$p = outRecord$p;	/* seems odd to share output buf */
	scmdch = '-';
	CALL rescan(1, .statusIO);	/* rewind to get command line args */
	cmd$p = scmd$p + 1;
	CALL readCmdLine;		/* read the command line args */
	cmd$p = skipspc(cmd$p);		/* skip leading space, DEBUG if present and loc command */
	IF strequ(cmd$p, .mdebug, 6) THEN
		cmd$p = skipspc(cmd$p + 6);
	cmd$p, args$p = skipspc(pastFileName(cmd$p));	/* args$p marks start of real args */
	DO WHILE cmdch <> 0DH;		/* read until end of args */
		IF cmdch = '&' THEN	/* handle continuation line */
		DO;
			p = cmd$p;	/* mark the & */
			cmd$p = skipspc(cmd$p + 1);	/* check we didn't have anything after */
			IF cmdch <> 0DH THEN
				CALL cmdErr(ERR203);	/* invalid syntax */
			cmd$p = p;	/* reset to & */
			CALL conStrOut(.mstar2, 2);	/* emit ** to user */
			cmdbuf(1) = 0DH;	/* put the cr lf ** after the & */
			cmdbuf(2) = 0AH;
			cmdbuf(3) = '*';
			cmdbuf(4) = '*';
			cmd$p = cmd$p + 5;	/* position to read the next line */
			CALL readCmdLine;	/* read another line */
		END;
		ELSE	/* skip char */
			cmd$p = cmd$p + 1;
	END;	/* DO WHILE */

	/* display the sign on message skipping the form feed */
	CALL constrOut(.signonMsg + 1, 23);
	CALL constrOut(.VERSION, 4);
	CALL constrOut(.crlf, 2);
	/* if we have run into the in buffer then we have a problem */
	IF cmd$p > sibuf$p THEN
		CALL cmdErr(ERR210);	/* insufficient memory */
	/* skip the leading space of the command args */
	CALL skipNonArgChars(args$p);
	/* get the file we are locating */
	CALL getFile;
	/* can only locate a disk file */
	IF spathInfo.deviceType <> 3 THEN
		CALL errNotADisk;
	CALL makeFullName(.spathInfo, .inFileName(1));
	/* convert to a omf style string by putting the length in at the front */
	inFileName(0) = pastFileName(.inFileName(1)) - .inFileName(1);
	/* check for TO (followed by space or & */
	IF strequ(cmd$p, .mto, 3) OR strequ(cmd$p, .mtoand, 3) THEN
	DO;
		CALL skipNonArgChars(cmd$p + 2);
		CALL getFile;	/* get the output file */
	END;
	ELSE
	DO;	/* handle the case of TO missing unless the input did not have an extension */
		IF spathInfo.ext(0) = 0 THEN
			CALL cmdErr(ERR233);	/* TO expected */
		spathInfo.ext(0) = 0;	/* assume TO would be input file name without extension */
		spathInfo.ext(1) = 0;
		spathInfo.ext(2) = 0;
	END;
	/* get the output file name in standard format */
	CALL makeFullName(.spathInfo, .outFileName(1));
	outFileName(0) = pastFileName(.outFileName(1)) - .outFileName(1);
	/* only accept a disk file or the bit bucket */
	IF spathInfo.deviceType <> 3 and spathInfo.deviceId <> 22 THEN	/* 22 -> :BB: */
		CALL errNotADisk;
	/* copy the disk name over and create the tmp file name */
	tmpFileInfo(0) = spathInfo.deviceId;
	CALL makefullName(.tmpFileInfo, .tmpFileName(1));
	tmpFileName(0) = pastFileName(.tmpFileName(1)) - .tmpFileName(1);
	/* record if we are creating a real file */
	outRealFile = spathInfo.deviceId <> 22;
	/* open the file to locate */
	CALL open(.readfd, .InFileName(1), 1, 0, .statusIO);
	CALL errChkReport(statusIO, .inFileName(1), TRUE);
	recNum = 0;
	/* check we have a relocation file */
	CALL getRecord;
	IF inRecord.rectyp <> R$MODHDR THEN
		CALL errChkReport(ERR239, .inFileName(1), TRUE);	/* no module header record */
	/* assume listing to :CO: */
	CALL pstrcpy(.cout, .printFileName);
	/* set other defaults and record that we haven't seen the commands */
	columns = 1;
	startSeen = 0;
	stackSizeSeen = 0;
	restart0Seen = 0;
	MapSeen = 0;
	PublicsSeen = 0;
	SymbolsSeen = 0;
	LinesSeen = 0;
	PurgeSeen = 0;
	moduleNameSeen = 0;
	isMain = 0;		/* assume not main module */
	modhdr$x1 = 0;
	modhdr$x2 = 0;
	startAddr = 0;
	/* initialise the segment order */
	CALL initSegOrder;
	/* and set all as not yet seen with 0 size and location */
	DO p = 0 TO 255;
		segFlags(p) = AUNKNOWN;
		segBases(p), segSizes(p) = 0;
	END;
	/* process the rest of the command args */
	DO WHILE cmdch <> 0DH;
		CALL processControls;
	END;
	/* no paging file yet and no allocated pages */
	havePagingFile = 0;
	roundRobinIndex = 0;
	pageIndexTmpFil = 0;
	/* recalculate pageCache size */
	pageCacheSize = HIGH(outRecord$p - (pageTab1$p := pageTab2$p + size(pageTab2))) - 1;
	/* set a new baseMemImage after allocting pageTab1 slots - one for each page */
	baseMemImage =  pageTab1$p + (pageCacheSize + 1) * 2 ;
	/* initialise the paging table control information */
	DO p = 0 TO 255;
		/* we have only allocated space for up to pageCacheSize entries in pageTab1 */
		IF p <= pageCacheSize THEN
			pageTab1(p).state = 0FFH; /* not used */
		pageTab2(p).pageIdx = 0FFH;	/* not used */
		pageTab2(p).fileIdx = 0FFH;	/* not used */
	END;
	/* allocate the print buffer below the output buffer if we have one */
	IF usePrintBuf THEN
		spbuf$p, pbuf$p = epbuf$p - npbuf;
	/* set up the heap locations below the print buffer */
	topHeap, topDataFrags, botHeap = spbuf$p;
	/* recalculate the pageCacheSize now available */
	pageCacheSize = HIGH(botHeap - baseMemImage) - 1;
	/* create the output file*/
	CALL delete(.outFileName(1), .statusIO);
	CALL open(.outputfd, .outFilename(1), 3, 0, .statusIO);
	CALL errChkReport(statusIO, .outFileName(1), TRUE);
	/* and the print file (or console) */
	CALL open(.printfd, .printFileName(1), 2, 0, .statusIO);
	CALL errChkReport(statusIO, .printFileName(1), TRUE);
	/* if not console then emit the signon and command line to the print file */
	IF printfd > 0 THEN
	DO;
		CALL printString(.signonMsg, 24);
		CALL printString(.VERSION, 4);
		CALL printString(.aInvokedBY, 14);
		CALL printString(.scmdch, cmd$p - .scmdch + 2);
	END;
END procArgs$Init;

DECLARE aInpageSegment2(*) BYTE PUBLIC INITIAL('INPAGE SEGMENT > 256 BYTES COERCED TO PAGE BOUNDRY', 0DH, 0AH),
	segId BYTE PUBLIC,
	pad7935(3) BYTE PUBLIC;

alignAddress: PROCEDURE(align, size, laddr) ADDRESS PUBLIC;
	DECLARE align BYTE;
	DECLARE (size, laddr) ADDRESS;
	IF size = 0 THEN	/* no size to no alignment needed */
		RETURN laddr;

	IF align = ABYTE THEN
		RETURN laddr;	/* no alignment needed */
	IF align = AINPAGE THEN
		IF size <= 256 THEN	/* check if fits in page */
		DO;
			IF HIGH(laddr) = HIGH(laddr + size - 1) THEN
				RETURN laddr;
		END;
		ELSE    /* inpage seg > 256 coerced to page boundary */
			CALL conAndPrint(.aInpageSegment2, 52);	
	RETURN (laddr + 0FFH) AND 0FF00H;	/* get a whole page */
END alignAddress;


procMODHDR: PROCEDURE PUBLIC;
	DECLARE rbyt BASED in$p (1) BYTE;
	DECLARE sdef BASED in$p STRUCTURE (segId BYTE, len ADDRESS, combine BYTE); 
	DECLARE (loadAddress, segSize, segOrderId) ADDRESS;
	DECLARE (atTopOfMem, loadHasSize) BYTE;

	IF NOT moduleNameSeen THEN	/* copy over the module name if not user overridden */
		CALL pstrcpy(in$p, .moduleName);
	in$p = in$p + rbyt(0) + 1;	/* past name */
	modhdr$x1 = rbyt(0);		/* copy the reserved bytes */
	modhdr$x2 = rbyt(1);
	in$p = in$p + 2;
	loadHasSize = 0;
	DO WHILE in$p < .erec;		/* process all of the seg info */
		IF (segFlags(segId := sdef.segId) AND AMASK) <> AUNKNOWN THEN
			CALL illegalRecord;	/* oops seen twice */
		IF segId <> SSTACK THEN		/* by default copy the length */
			segSizes(segId) = sdef.len;
		ELSE IF NOT stackSizeSeen THEN	/* for stack copy if not user overridden */
			segSizes(SSTACK) = sdef.len;
		IF segSizes(segId) > 0 THEN	/* check we have some data */
			loadHasSize = TRUE;
		IF sdef.combine - 1 > ABYTE - 1 THEN	/* check valid alignment */
			CALL illegalRecord;
		segFlags(segId) = segFlags(segId) + sdef.combine;	/* set the combine info */
		in$p = .sdef + size(sdef);	/* skip to next seg info */
	END;
	IF (segFlags(SSTACK) AND AMASK) = AUNKNOWN THEN		/* if STACK not specified put default */
		segFlags(SSTACK) = segFlags(SSTACK) + ABYTE;	/* set to byte align */
	IF (segFlags(SMEMORY) AND AMASK) = AUNKNOWN THEN	/* if MEMORY not specified put default */
		segFlags(SMEMORY) = segFlags(SMEMORY) + ABYTE;	/* set to byte align */
	IF NOT stackSizeSeen AND loadHasSize THEN		/* it STACK not specified by user and we have data */
		segSizes(SSTACK) = segSizes(SSTACK) + 12;	/* reserve an additional 12 bytes */
	loadAddress = 3680H;				/* ISIS load address */
	atTopOfMem = 0;

	DO segOrderId = 1 TO 254;	/* process the segs taking account of user specified order */
		segid = segOrder(segOrderId);
		segSize = segSizes(segId);	/* pick up seg to process */
		IF (segFlags(segId) AND FHASADDR) = 0 THEN	/* no address specified */
		DO;
			IF atTopOfMem <> 0 THEN	/* check for going over 64k */
			DO;
				IF segSize > 0 THEN	/* program exceeds 64k */
					CALL errChkReport(ERR240, .inFileName(1), TRUE);
				segFlags(segId) = segFlags(segId) OR FWRAP0;
			END;
		END;
		ELSE
		DO;	/* user specified address */
			IF segSize > 0 THEN	/* reset topOfMemflag */
				atTopOfMem = 0;
			loadAddress = segBases(segId);	/* set the loadAddress to user specified */
		END;
		/* update this segments base address allowing for alignment */
		segBases(segId) = alignAddress(segFlags(segId) AND AMASK, segSize, loadAddress);
		IF segId = SMEMORY THEN
			IF segSize = 0 AND loadHasSize THEN	/* if not specified and program has size */
								/* calcualte a MEMORY size */
				IF atTopOfMem = 0 AND memck > segBases(SMEMORY) THEN
					segSizes(SMEMORY), segSize = memck - segBases(SMEMORY);
		/* advance loadAddress and check for memory wrap around */
		IF (loadAddress := segBases(segId) + segSize) < segBases(segId) THEN
			IF loadAddress = 0 THEN		/* at 64k ok else error */
				atTopOfMem = FWRAP0;
			ELSE
				CALL errChkReport(ERR240, .inFileName(1), TRUE);  /* exceeds 64k */
	END; 
	segBases(SSTACK) = segBases(SSTACK) + segSizes(SSTACK);	/* stack goes down so update to top */
	CALL getRecord;	/* preload next record */
END procMODHDR;


procCOMDEF: PROCEDURE PUBLIC;
	DECLARE	comdefRec BASED in$p STRUCTURE (segid BYTE, name(1) BYTE);

	DO WHILE in$p < .erec;
		IF (segId := comdefRec.segid) < SNAMED OR segId = SBLANK THEN
			CALL illegalRecord;
		/* check if combine value not appropriate for common or if this one already seen */
		IF ((segFlags(segId) AND AMASK) = AUNKNOWN) OR ((segFlags(segId) AND FSEGSEEN) <> 0)THEN
			CALL illegalRecord;
		segFlags(segId) = segFlags(segId) OR FSEGSEEN;	/* flag as seen */
		in$p = in$p + 2 + comdefRec.name(0);		/* past byte, len, string */
	END;
	CALL getRecord;
END procCOMDEF;


procHdrAndComDef: PROCEDURE PUBLIC;
	CALL objSeek(0, 0);	/* rewind */
	CALL getRecord;		/* get modhdr */
	CALL procMODHDR;	/* load the modhdr and set the segment bases */

	DO WHILE inRecord.rectyp = R$COMDEF;	/* process any comdefs */
		CALL procCOMDEF;
	END;
	DO segId = SNAMED TO SBLANK - 1;	/* check commons in modhdr have comdef entries */
		IF ((segFlags(segId) AND AMASK) <> AUNKNOWN) AND ((segFlags(segId) AND FSEGSEEN) = 0) THEN
			CALL badRecordSeq;
	END;
END procHdrAndComDef;

END locate7;
loc8.plm
locate8: DO;
$include(loc8.ipx)

DECLARE devices (*) BYTE DATA('F0F1F2F3F4F5TITO',
		      'VIVOI1O1TRHRR1R2',
		      'TPHPP1P2LPL1BBCI',
		      'COF6F7F8F9');

/* convert the spath info into a standard file name :xx:file.ext */
makeFullName: PROCEDURE(pinfo, pstr) PUBLIC;
	DECLARE pinfo ADDRESS;
	DECLARE pstr ADDRESS;
	DECLARE str BASED pstr(1) BYTE;
	DECLARE ch BASED pstr BYTE;
	DECLARE info BASED pinfo SPATH$T;

	DECLARE i BYTE;

	/* put the :xx: */
	str(0) = ':';
	str(1) = devices(info.deviceId + info.deviceId);
	str(2) = devices(info.deviceId + info.deviceId + 1);
	str(3) = ':';
	pstr = pstr + 4;	/* past the :xx: inserted */
	/* put the file */
	DO i = 0 TO 5;
		IF (ch := info.name(i)) <> 0 THEN
			pstr = pstr + 1;
	END;
	/* only put .ext if it exists */
	IF info.ext(0) <> 0 THEN
	DO;
		ch = '.';
		pstr = pstr + 1;
	END;
	/* put the ext - could have been in the if clause above */
	DO i = 0 TO 2;
		IF (ch := info.ext(i)) <> 0 THEN
			pstr = pstr + 1;
	END;
	ch = ' ';	/* space at end */
END makeFullName;


strupr: PROCEDURE(pch) PUBLIC;
	DECLARE pch ADDRESS;
	DECLARE ch BASED pch BYTE;

	DO WHILE ch <> 0DH;
		ch = toupper(ch);
		pch = pch + 1;
	END;
END strupr;

END locate8;
loc8a.plm
locate8: DO;
$include(loc8a.ipx)

DECLARE hexch(*) BYTE DATA ('0123456789ABCDEF');

parseNumber: PROCEDURE(ppstr) ADDRESS PUBLIC;
	DECLARE ppstr ADDRESS;
	DECLARE pstr BASED ppstr ADDRESS;
	DECLARE (pch, pafter, pendNum) ADDRESS;
	DECLARE (i, radix, digit) BYTE;
	DECLARE (num, lastnum) ADDRESS;
	DECLARE ch BASED pch BYTE;

	pch = pstr;
	DO WHILE ch = ' ';	/* skip spaces */
		pch = pch + 1;
	END;
	pstr = pch;		/* update the source pstr */

	DO WHILE '0' <= ch AND ch <= '9' OR 'A' <= ch AND ch <= 'F';
		pch = pch + 1;
	END;
	pafter = (pendNum := pch) + 1;
	IF ch = 'H' THEN
		radix = 16;
	ELSE IF ch = 'O' OR ch = 'Q' THEN
		radix = 8;
	ELSE
	DO;	/* check for D or B or numeric */
		pendNum, pch = (pafter := pch) - 1;
		IF ch = 'B' THEN
			radix = 2;
		ELSE
		DO;
			radix = 10;
			IF ch <> 'D' THEN	/* if a digit then include in number */
				pendNum = pendNum + 1;
		END;
	END;
	pch = pstr;	/* reset to start */
	num, lastnum = 0;
	DO WHILE pch < pendNum;
		DO i = 0 TO 15;
			IF ch = hexch(i) THEN
				digit = i;
		END;
		IF digit >= radix THEN
			RETURN 0;	/* illegal number */
		IF (num := lastnum * radix + digit) < lastnum THEN
			RETURN 0;	/* overflow */
		lastnum = num;
		pch = pch + 1;
	END;
	pstr = pafter;
	RETURN num;
END parseNumber;


strequ: PROCEDURE(pstr1, pstr2, len) BYTE PUBLIC;
	DECLARE (pstr1, pstr2) ADDRESS, len BYTE;
	DECLARE i BYTE;
	DECLARE str1 BASED pstr1 (1) BYTE, str2 BASED pstr2 (1) BYTE;

	len = len - 1;
	DO i = 0 TO len;
		IF str1(i) <> str2(i) THEN
			RETURN FALSE;
	END;
	RETURN TRUE;
END strequ;


END locate8;
loc9.plm
locate9:
DO;
toupper: PROCEDURE(ch) BYTE PUBLIC;
	DECLARE ch BYTE;
	IF ch < 'a' THEN
		RETURN ch;
	IF ch > 'z' THEN
		RETURN ch;
	RETURN ch AND 0DFH;
END toupper;

END locate9;
makefile
TARGET=locate
# common makefile info
ISIS=..\..\thames
V4=../../plm80v4/
V3=../../plm80v3/
F0=./
TOOLS=..\..\tools
PEXFILE=loc80.pex
#
# compilation and assembly rules
.SUFFIXES: .plm .pl3
.plm.obj:
	@SET ISIS_F0=$(F0)
	@SET ISIS_F1=$(V4)
	$(ISIS) :F1:PLM80 $< code
	@if not exist $*.obj exit /b 1

.pl3.obj:
	@SET ISIS_F0=$(F0)
	@SET ISIS_F1=$(V3)
	$(ISIS) :F1:PLM80 $< code
	@find $*.lst "     0 PROGRAM ERROR(S)" >nul || (del $*.obj & exit /b 1)

.asm.obj:
	@SET ISIS_F0=$(F0)
	@SET ISIS_F1=$(V4)
	$(ISIS) :F1:ASM80 $< 
	@find $*.lst "ASSEMBLY COMPLETE,   NO ERRORS" >nul || (del $*.obj & exit /b 1)

.plm.ipx:
	@SET ISIS_F0=$(F0)
	@SET ISIS_F1=$(V4)
	@SET ISIS_F3=$(F3)
	$(TOOLS)\ngenpex $(PEXFILE) $<

.pl3.ipx:
	@SET ISIS_F0=$(F0)
	@SET ISIS_F1=$(V4)
	@SET ISIS_F3=$(F3)
	$(TOOLS)\ngenpex $(PEXFILE) $<

LOWOBJ=loc1.obj loc2a.obj memmov.obj loc2b.obj loc3.obj loc4.obj loc4a.obj loc5a.obj loc5b.obj isis.obj
HIGHOBJ=loc6.obj loc7.obj loc8.obj loc8a.obj loc9.obj

# locate is built in two parts the value of the macro is the base address of the higher part
# it should be the same as the MEMORY address at the end of the loctmp.map file
HICODE=6656H

# locate build rules
# default target
all:	.depend
all:
	$(MAKE) $(TARGET)

$(TARGET): loctmp.abs $(HIGHOBJ)
	@SET ISIS_F0=$(F0)
	@SET ISIS_F1=$(V4)
	$(ISIS) :F1:link "&" < <<
$(**: =,&
),:f1:system.lib,:f1:plm80.lib to $*.rel
<<NOKEEP
	$(ISIS) :F1:locate $*.rel to locate MAP PRINT($*.map) SYMBOLS PUBLICS STACKSIZE(0) CODE($(HICODE)) PURGE
	@del $*.rel $*.abs
	@(find $*.map "UNSATISFIED EXTERNAL" >NUL && exit /b 1) || exit /b 0

# intermediate target - note will have unsatisfied externals see also HICODE above
loctmp.abs: $(LOWOBJ)
	@SET ISIS_F0=$(F0)
	@SET ISIS_F1=$(V4)
	echo $(**: =,)
	$(ISIS) :F1:link "&" < <<
$(**: =,&
),:f1:plm80.lib to $*.rel 
<<NOKEEP
	$(ISIS) :F1:locate $*.rel to $@ STACKSIZE(6CH) map print(loctmp.map)
	@del $*.rel
	@if not exist $@ exit /b 1

$(LOWOBJ:.obj=.ipx) : $(PEXFILE)
$(HIGHOBJ:.obj=.ipx) : $(PEXFILE)

.depend: loc80_all.plm
	..\..\unpack.pl
	..\..\mkdepend.pl
	date /t >.depend

clean:
	del *.obj *lst *.map *.abs *.rel *.*~ *~

vclean: clean
	del *.ipx $(TARGET) 

uclean: vclean
	del $(LOWOBJ:.obj=.plm) $(HIGHOBJ:.obj=.plm) *.asm *.pex makefile .depend

gitprep: uclean
	..\..\unpack.pl

# make this target to check the build matches the original binary
verify: all
	fc /b $(TARGET) $(V4)$(TARGET)



memmov.asm
	PUBLIC memmov
	CSEG
; mem$move(count, src$p, dst$p)
memmov: pop	h
	xthl
	push	b
	mov	b, h
	mov	c, l
	pop	h
	dcx	b
	mov	a, e
	sub	l
	mov	a, d
	jz	l410F
	sbb	h
	jmp	l4111

l410F:	sbb	h
	rz

l4111:	push	h
	lxi	h, l4167
	jc	l4121
	pop	h
	dad	b
	push	h
	xchg
	dad	b
	xchg
	lxi	h, l4190

l4121:	inx	b
	mov	a, b
	rrc
	rrc
	rrc
	mov	b, a
	mvi	a, 7
	ana	c
	push	psw
	xra	c
	rrc
	rrc
	rrc
	xra	b
	ani	1Fh
	xra	b
	mov	c, a
	mvi	a, 1Fh
	ana	b
	mov	b, a
	inr	b
	inr	c
	pop	psw
	rlc
	rlc
	push	b
	cma
	mov	c, a
	mvi	b, 0FFh
	inx	b
	dad	b
	pop	b
	xthl
	ret

l4147:	mov	a, m
	stax	d
	inx	h
	inx	d
	mov	a, m
	stax	d
	inx	h
	inx	d
	mov	a, m
	stax	d
	inx	h
	inx	d
	mov	a, m
	stax	d
	inx	h
	inx	d
	mov	a, m
	stax	d
	inx	h
	inx	d
	mov	a, m
	stax	d
	inx	h
	inx	d
	mov	a, m
	stax	d
	inx	h
	inx	d
	mov	a, m
	stax	d
	inx	h
	inx	d

l4167:	dcr	c
	jnz	l4147
	dcr	b
	jnz	l4147
	ret

l4170:	mov	a, m
	stax	d
	dcx	h
	dcx	d
	mov	a, m
	stax	d
	dcx	h
	dcx	d
	mov	a, m
	stax	d
	dcx	h
	dcx	d
	mov	a, m
	stax	d
	dcx	h
	dcx	d
	mov	a, m
	stax	d
	dcx	h
	dcx	d
	mov	a, m
	stax	d
	dcx	h
	dcx	d
	mov	a, m
	stax	d
	dcx	h
	dcx	d
	mov	a, m
	stax	d
	dcx	h
	dcx	d

l4190:	dcr	c
	jnz	l4170
	dcr	b
	jnz	l4170
	ret
	END
