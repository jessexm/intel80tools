page: DO;
declare lit literally 'literally',
	true lit '0ffh',
	false lit '0',
	cr lit '0dh',
	lf lit '0ah';

$nolist
$include(:f3:data.inc)
wrnstrLst: procedure(str, cnt) external; declare str address, cnt address; end;
wrcLst: procedure(ch) external; declare ch byte; end;
wr2cLst: procedure(ch2) external; declare ch2 address; end;
$include(:f3:itoa.pex)
$list

newPgl: procedure public;
	declare (i, j, k, m) byte,
		pnum(3) byte;

	if not PAGING then
		return;
	call wrcLst(0ch);
	linlft = PAGELEN;
	pageNo = pageNo + 1;
	i = num2Asc(pageNo, 3, 10, .pnum);
	j = PWIDTH - 41;
	if j < TITLELEN then
		m = j;
	else
		m = TITLELEN;
	call wrnstrLst(.Plm80Compiler, 20);
	call wrnstrLst(.TITLE, m);
	k = j - m + 2;
	do while k <> 0;
		call wrcLst(' ');
		k = k - 1;
	end;
	call wrnstrLst(.DATE, 9);
	call wrnstrLst(.('  PAGE '), 7);
	call wrnstrLst(.pnum, 3);
	call wrnstrLst(.(cr, lf, lf, lf), 4);
	b3CFF = 0;
end;

end;
