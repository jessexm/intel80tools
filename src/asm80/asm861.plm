asm861: do;

$include(global.inc)

declare bZERO byte external;

sourceError: procedure(b) external; declare b byte; end;
runtimeError: procedure(b) external; declare b byte; end;
getChClass: procedure byte external; end;
getCh: procedure byte external; end;



syntaxError: procedure public;
	call sourceError('Q');	/* syntaxError */
end;

doubleDefError: procedure public;
	call sourceError('D');	/* double defined symbol */
end;

expressionError: procedure public;
	call sourceError('E');	/* external error */
end;

commandError: procedure public;
	call sourceError('C');	/* command error */
end;

OpcodeOperandError: procedure public;
	call sourceError('O');
end;

nameError: procedure public;
	call sourceError('R');
end;

multipleDefError: procedure public;
	call sourceError('M');	/* multiply defined symbol */
end;

valueError: procedure public;
	call sourceError('V');	/* value error */
end;

nestingError: procedure public;
	call sourceError('N');	/* nesting error */
end;

phaseError: procedure public;
	call sourceError('P');	/* phase error */
end;

stackError: procedure public;
	call runtimeError(0);	/* stack error */
end;

fileError: procedure public;
	call runtimeError(4);	/* file error */
end;

illegalCharError: procedure public;
	call sourceError('I');
end;

balanceError: procedure public;
	call sourceError('B');
end;

undefinedSymbolError: procedure public;
	call sourceError('U');	/* undefined symbol */
end;

locationError: procedure public;
	call sourceError('L');
end;

operandError: procedure public;
	call sourceError('X');
end;

haveTokens: procedure byte public;
	return tokenSP <> 0;
end;


popToken: procedure public;
	tokStart(0) = tokStart(tokenSP);
	tokenSym(0) = tokenSym(tokenSP);
	tokenType(0) = tokenType(tokenSP);
	tokenAttr(0) = tokenAttr(tokenSP);
	tokenSize(0) = tokenSize(tokenSP);
	tokenSymId(0) = tokenSymId(tokenSP);
	if haveTokens then
		tokenSP = tokenSP - 1;
end;


nestIF: procedure(arg1b) public;
	declare arg1b byte;
	
	if (ifDepth := ifDepth + 1) > 8 then
	do;
		call stackError;
		ifDepth = 0;
	end;
	else
	do;
		skipping(ifDepth) = skipping(0);
		inElse(ifDepth) = inElse(0);
	end;
end;

unnestIF: procedure(arg1b) public;
	declare arg1b byte;

	if ifDepth = 0 then
		call nestingError;
	else
	do;
		skipping(0) = skipping(ifDepth);
		inElse(0) = inElse(ifDepth);
		ifDepth = ifDepth - 1;
	end;
end;

pushToken: procedure(arg1b) public;
	declare arg1b byte;
	if tokenSP >= 8 then
		call stackError;
	else
	do;
		tokenSP = tokenSP + 1;
		tokStart(tokenSP) = tokStart(0);
		tokenSym(tokenSP) = tokenSym(0);
		tokenType(tokenSP) = tokenType(0);
		tokenAttr(tokenSP) = tokenAttr(0);
		tokenSize(tokenSP) = tokenSize(0);
		tokenSymId(tokenSP) = tokenSymId(0);
		tokStart(0) = tokStart(0) + tokenSize(0);	/* advance for next token */
		tokenType(0) = arg1b;
		tokenAttr(0), tokenSize(0) = bZERO;
		tokenSym(0) = wZERO;
		tokenSymId(0) = tokenSym(0);
	end;
end;

addByte2Tok: procedure(arg1b) public;
	declare arg1b byte, s address;
	declare ch based s byte;

	if (s := tokStart(0) + tokenSize(0)) < endLineBuf then	/* check for lineBuf overrun */
	do;
		ch = arg1b;
		tokenSize(0) = tokenSize(0) + 1;
	end;
	else
		call stackError;
end;

getId: procedure(arg1b) public;
	declare arg1b byte;

	call pushToken(arg1b);
	reget = 1;

	do while (arg1b := getChClass) = CC$DIGIT or arg1b = CC$LET;	/* digit or letter */
		if curChar > 60h then			/* make sure upper case */
			curChar = curChar and 0DFh;
		call addByte2Tok(curChar);
	end;
	reget = 1;
end;


getNum: procedure public;
	declare accum address, (radix, digit, i) byte;
	declare chrs based curTokStart(1) byte;

	call getId(0Ch);
	

	radix = chrs(tokenSize(0) := tokenSize(0) - 1);
	if radix = 'H' then
		radix = 10h;

	if radix = 'D' then
		radix = 0Ah;

	if radix = 'O' or radix = 'Q' then
		radix = 8;

	if radix = 'B' then
		radix = 2;

	if radix > 10h then
		radix = 10;
	else
		tokenSize(0) = tokenSize(0) - 1;

	accum = 0;
	do i = 0 to tokenSize(0);
		if chrs(i) = '?' or chrs(i) = '@' then
		do;
			call illegalCharError;
			digit = 0;
		end;
		else
		do;
			digit = chrs(i) - '0';
			if digit > 9 then
				digit = digit - 7;
			if digit >= radix then
				if tokenType(2) <> 40h then
				do;
					call illegalCharError;
					digit = 0;
				end;
		end;

		accum = accum * radix + digit;
	end;

	/* replace with packed number */
	tokenSize(0) = 0;
	call addByte2Tok(low(accum));
	call addByte2Tok(high(accum));
end;


getStr: procedure public;
	call pushToken(0Ah);

	do while getCh <> 0Dh;
		if curChar = 27h then
			if getCh <> 27h then
				goto L6268;
		call addByte2Tok(curChar);
	end;

	call balanceError;

L6268:
	reget = 1;
end;

end;
